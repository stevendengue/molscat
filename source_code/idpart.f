      SUBROUTINE IDPART(ITYPE,IDENT,SPNUC,WT)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION WT(2)
C
C  THIS ROUTINE PROCESSES INPUT FOR IDENTICAL PARTICLES.
C  IDENT.NE.0 IMPLIES TWO PARTICLES ARE IDENTICAL.
C  OTHERWISE (IDENT.EQ.0) IEXCH=0 IN ALL CASES AND NO PROCESSING
C  WITH IEXCH OR WT WILL OCCUR.
C
      CHARACTER(8) NAME(2)
      DATA NAME/'  BOSE  ',' FERMI  '/
C
      IF (IDENT.EQ.0)  RETURN
      WRITE(6,600)
  600 FORMAT(/'  IDENT PARAMETER SPECIFIES THAT TWO PARTICLES',
     1       ' ARE IDENTICAL.  PROPERLY SYMMETRIZED FUNCTIONS WILL ',
     2       'BE CONSTRUCTED.')
      IF (ITYPE.EQ.3 .OR. ITYPE.EQ.13 .OR. ITYPE.EQ.23) GOTO 1000

      WRITE(6,601) ITYPE
  601 FORMAT(/'  * * * ERROR. FOR ITYPE =',I4,'  IDENT PROCESSING ',
     1       'NOT SUPPORTED.  REQUEST CANCELLED.')
      IDENT=0
      RETURN

 1000 SPNUC=ABS(SPNUC)
      IF (WT(1).EQ.0.D0 .AND. WT(2).EQ.0.D0) GOTO 2000

      WRITE(6,603) WT
  603 FORMAT('  STATISTICAL WEIGHTS SPECIFIED AS WT IN &BASIS DATA.',
     1       '  SPNUC IGNORED.'/10X,'ANTI-SYMMETRIC, WT(1) =',F7.4,
     2       ',  SYMMETRIC, WT(2) =',F7.4)
      RETURN

 2000 IST=INT(2.D0*SPNUC+0.0001D0)
      IST=IST-2*(IST/2)
C  IST=0 FOR BOSE STATISTICS, IST=1 FOR FERMI STATISTICS.
      DN=2.D0*SPNUC+1.D0
      WT(2-IST)=(SPNUC+1.D0)/DN
      WT(IST+1)=SPNUC/DN
      WRITE(6,602) SPNUC,NAME(IST+1),WT
  602 FORMAT('  FOR NUCLEAR SPIN =', F6.2,',',A8,' STATISTICAL ',
     1       'WEIGHTS ARE'/10X,'ANTI-SYMMETRIC, WT(1) =',F7.4,
     2       ',   SYMMETRIC, WT(2) =',F7.4)
      RETURN
      END
