      SUBROUTINE DELRD(DR,CDIAG,COFF,TOL,DRMAX,
     1                 E1,EN,RNOW,RMAX)
C  This subroutine is part of the MOLSCAT, BOUND and FIELD suite of programs
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  THIS ROUTINE IS A MODIFIED VERSION OF ROY GORDON'S QCPE PROGRAM.
C  THIS VERSION FOR SCATTERING CALCULATIONS
C
C  ADJUST THE STEP SIZE TO TRY TO KEEP MAX(CDIAG,COFF) = TOL
      RTOL = 0.80D0*TOL
C
C  FIND CORRECTION FACTOR FROM DIAGONAL PERTURBATIONS
      IF (CDIAG.NE.0.D0) GOTO 100
C
C  CASE IN WHICH DIAGONAL PERTURBATIONS VANISH
      DFACT = 2.5D0
      GOTO 110
C
C  DIAGONAL PERTURBATIONS VARY ROUGHLY AS THE FIFTH POWER OF STEP SIZE
C
C  - CRLS 2014 -
C  THIS RECIPE BELOW MATCHES EQN 29 OF ALEXANDER JCP 81, 4510 (1984),
C  NOT WITH THE COMMENT ABOVE. PROVENANCE OF COMMENT ABOVE IS UNCLEAR.
  100 DFACT = (RTOL/CDIAG)**0.333D0
C
C  FIND CORRECTION FACTOR FROM OFF-DIAGONAL PERTURBATIONS
  110 IF (COFF.NE.0.D0) GOTO 120
C
C  CASE IN WHICH OFF-DIAGONAL PERTURBATIONS VANISH
      OFACT = 2.5D0
      GOTO 130
C
C  OFF-DIAGONAL PERTURBATIONS VARY ROUGHLY AS CUBE OF STEP SIZE
C
C  - CRLS 2014 -
C  THIS RECIPE BELOW MATCHES WITH EQN 27 OF ALEXANDER PAPER.
  120 OFACT = (RTOL/COFF)**0.333D0
C
C  FIND MINIMUM FACTOR
  130 FACTOR = MIN(DFACT,OFACT)
      IF (EN.GT.0.D0) GOTO 150
      IF (E1.GT.0.D0) GOTO 140
C
C  THIS IS REACHED ONLY WHEN ALL CHANNELS ARE IN THEIR CLASSICALLY
C  FORBIDDEN REGIONS.  THEN ACCURACY IS QUITE SENSITIVE TO CHANGES
C  IN STEP SIZE.
C  HENCE IN THIS REGION MAKE ONLY CAUTIOUS CHANGES IN STEP SIZE
      IF (FACTOR.GT.1.15D0) FACTOR = 1.15D0
      GOTO 170
C
C  THIS IS REACHED WHEN SOME CHANNELS ARE CLASSICAL AND OTHERS NOT
  140 IF (FACTOR.GT.1.20D0) FACTOR = 1.20D0
      GOTO 170
C
C  THIS IS REACHED WHEN ALL CHANNELS ARE CLASSICAL.
C  THEN THE STEP SIZE IS OFTEN INCREASING RAPIDLY, AND ALSO THE
C  ACCURACY VARIES MORE SLOWLY WITH STEP SIZE.
C  THUS WE MAKE BOLDER INCREASES IN THE STEP SIZE, TO KEEP THE
C  CORRECTIONS OF THE SAME ORDER OF MAGNITUDE AS BEFORE
C  TEST TO SEE HOW FAR WE HAVE INTEGRATED
  150 IF (RNOW.GT.(0.10D0*RMAX)) GOTO 160
      IF (FACTOR.GT.1.6D0) FACTOR = 1.6D0
      GOTO 170
C
C  CHOOSE FACTOR IN FAR ASYMPTOTIC REGION
  160 IF (FACTOR.GT.2.5D0) FACTOR = 2.5D0
C
C  SET NEW STEP SIZE
  170 DR = DR*FACTOR
C
C  CHECK AGAINST DRMAX AND EXCESSIVE GROWTH OF CLOSED CHANNELS
      IF (EN.GE.0.D0) GOTO 175

      DREXP = 4.D0/SQRT(-EN)
      IF (DR.GT.DREXP) DR = DREXP
  175 IF (DR.GT.DRMAX) DR = DRMAX
      IF (DR.LT.1.0D-06*DRMAX) GOTO 180
      RETURN

  180 WRITE(6,1000) DR,CDIAG,COFF,TOL,DRMAX,E1,EN,RNOW,RMAX
      STOP
 1000 FORMAT(/' * * * ERROR IN DELRD.  STEP SIZE =',E20.6,
     1       ' IS TOO SMALL'//24X,'PARAMETERS PASSED ARE',
     2       '  CDIAG, COFF, TOL, DRMAX, E1, EN, RNOW, RMAX'/
     3       25X,9(E10.3,1X))
      END
