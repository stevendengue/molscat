      SUBROUTINE CHEINT(EINT,DGVL,N,OLDFAC,CINT)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  Written by C R Le Sueur Sep 2018
C  THIS SUBROUTINE ALTERS EINT FROM ITS VALUE ON ENTRY TO THE VALUE FOR
C  THE CURRENT SET OF CONDITIONS (EFVs MOSTLY).
C
C  ON ENTRY: EINT   CONTAINS THE INTERNAL ENERGY (FROM PREVIOUS LOOP
C                            OVER EFVs OR FROM INITIALISATION)
C            DGVL   CONTAINS FACTORS WHICH, WHEN MULTIPLIED BY THE VALUES
C                            IN VCONST, GIVE CONTRIBUTIONS TO THE INTERNAL
C                            ENERGY OF EACH BASIS FUNCTION
C            N    IS THE SIZE OF THE CURRENT BASIS
C            OLDFAC CONTAINS THE VALUES OF VCONST FROM THE PREVIOUS LOOP
C                            OVER EFVs (IT CONTAINS ZEROS BEFORE THE LOOP
C                            BEGINS)
C            CINT IS THE FACTOR THAT CONVERTS EINT INTO INTERNAL REDUCED
C                 UNITS FROM CM-1
C
C  ON EXIT: EINT   CONTAINS THE CURRENT VALUES FOR THE INTERNAL ENERGY
C           OLDFAC CONTAINS THE CURRENT VALUES OF VCONST
      USE efvs
      USE potential
      IMPLICIT NONE

      SAVE
      DOUBLE PRECISION, INTENT(INOUT) :: EINT(N),OLDFAC(MXOMEG)
      DOUBLE PRECISION, INTENT(IN)    :: DGVL(N,NDGVL),CINT
      INTEGER,          INTENT(IN)    :: N

      INTEGER I,J

      IF (NCONST.NE.0) RETURN
      DO I=1,N
        DO J=1,NDGVL
          EINT(I)=EINT(I)+CINT*(VCONST(J)-OLDFAC(J))*DGVL(I,J)
        ENDDO
      ENDDO

      DO J=1,NDGVL
        OLDFAC(J)=VCONST(J)
      ENDDO

      RETURN
      END
