      SUBROUTINE EVMTCH(PSI,EIGMIN,IMIN,JSTATE,L,
     1                  JSINDX,N,NSTATE,NQN,IPRINT)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      IMPLICIT NONE
C
C  SUBROUTINE TO PRINT THE EIGENVECTOR CORRESPONDING TO A NEAR-ZERO
C  EIGENVALUE.
C
C  ON ENTRY: PSI CONTAINS THE EIGENVECTOR CORRESPONDING TO THE SMALLEST
C                EIGENVALUE OF THE MATCHING MATRIX,
C            EIGMIN IS THE SMALLEST EIGENVALUE OF THE MATCHING MATRIX,
C            IMIN IS ITS INDEX,
C            JSTATE CONTAINS THE BASIS FUNCTION LABELS,
C            L CONTAINS THE L QUANTUM NUMBER FOR EACH BASIS FUNCTION,
C            JSINDX CONTAINS THE POINTERS TO THE BASIS FUNCTIONS,
C            N IS THE SIZE OF THE BASIS,
C            NSTATE IS THE SIZE OF THE COMPLETE BASIS,
C            NQN IS THE NUMBER OF BASIS FUNCTION LABELS (+1),
C            IPRINT CONTROLS VERBOSITY OF OUTPUT.

      INTEGER, INTENT(IN) :: IMIN,NSTATE,NQN,JSTATE(NSTATE,NQN),N,L(N),
     1                       JSINDX(N),IPRINT
      DOUBLE PRECISION, INTENT(INOUT):: PSI(N),EIGMIN

      INTEGER I,K,JJ
      DOUBLE PRECISION CRIT,P

      IF (IPRINT.EQ.5 .OR. IPRINT.EQ.6) WRITE(6,1001) IMIN,EIGMIN
      IF (IPRINT.GE.7) WRITE(6,1001) IMIN
      IF (IPRINT.GE.5) WRITE(6,1002)
 1001 FORMAT(/'  SMALLEST EIGENVALUE OF MATCHING MATRIX IS NUMBER',I3,:
     1        ' = ',1PG12.5)
 1002 FORMAT(/'  WITH PRINCIPAL EIGENVECTOR COMPONENTS'/
     1        '  CHAN     VEC    VEC**2   L   QUANTUM NUMBERS'/)

      CRIT=0.5D0/DBLE(N)
      DO I=1,N
        P=PSI(I)**2
        JJ=JSINDX(I)
C  WRITE OUT (A LIMITED SUBSET OF) THE COMPONENTS OF THIS EIGENVECTOR
        IF (P.GT.CRIT .OR. IPRINT.GE.11)
     1    WRITE(6,1003) I,PSI(I),P,L(I),(JSTATE(JJ,K),K=1,NQN)
 1003     FORMAT(I4,2F10.4,8I4)
      ENDDO

      RETURN
      END
