      SUBROUTINE EFPROP(N, RBEGIN, REND, NSTEP, PSIB,
     1                  RAB, PSIA, IWREC, SUMPSI, IPRINT, IPREC)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  ROUTINE TO PROPAGATE THE EIGENFUNCTION (WAVEFUNCTION) AT THE MATCHING
C  POINT GENERATING A VECTOR FOR IT AT EACH STEP IN THE PROPAGATION.
C  THE COUPLING MATRIX EVALUATED AT THE MIDPOINT OF EACH SECTOR
C  IS USED AS A REFERENCE POTENTIAL FOR THE SECTOR. THE MODIFIED
C  LOG DERIVATIVE PROPAGATOR OF MANOLOPOULOS IS USED. THIS VERSION
C  ADAPTED FROM DAPROP BY AE Thornley NOV 92
C
C  SIMPSONS RULE INTEGRATION CORRECTED CR Le Sueur MAY 12
C  ROUTINE SIMPLIFIED TOO
C
C  FURTHER ADAPTIONS TO DEAL WITH THE POSSIBILITY OF MULTIPLE PROPAGATION
C  SEGMENTS BY CR Le Sueur NOV 2016
C
C  NOV 2016 CR Le Sueur
C  CHANGES PREPARATORY TO USING QUADRATURE WITH UNEQUAL SPACES:
C  R IS WRITTEN TO PROPAGATION FILE BY MDPROP, AND READ IN HERE, AND IS
C  ALSO WRITTEN OUT TO WAVEFUNCTION FILE.
C
C  ON ENTRY: RBEGIN}
C            REND  } ARE LIMITS OF RADIAL PROPAGATION VARIABLE,
C            N       IS THE SIZE OF THE BASIS,
C            NSTEP   IS THE NUMBER OF STEPS USED IN THE RADIAL PROPAGATION
C            PSIA    IS THE WAVEFUNCTION TO START PROPAGATING FROM
C            RAB     IS THE PROPAGATION MATRIX
C            IWREC   IS THE ADDRESS OF THE LAST W (RAB) MATRIX WRITTEN TO
C                    CHANNEL IWAVSC
C            IPREC   IS THE ADDRESS OF WHERE THE FIRST PSI IS TO BE WRITTEN
C                    TO ON CHANNEL IPSISC
C  DURING:   PSIA}
C            PSIB}   ARE WORKSPACE ARRAYS USED FOR THE WAVEFUNCTION
C
C  ON EXIT:  SUMPSI  CONTAINS THE ACCUMULATED NORMALISATION FACTORS
C
      DIMENSION PSIA(N),PSIB(N), SUMPSI(N), RAB(N,N)
      COMMON /IOCHAN/ IPSISC,IWAVSC,IPSI,NWCOL,PSIFMT
      LOGICAL PSIFMT
C
C  THIS VERSION USES A CONSTANT STEP SIZE THROUGHOUT THE
C  INTEGRATION RANGE, WITH NSTEP STEPS BETWEEN RBEGIN AND REND.
C
      H=(REND-RBEGIN)/DBLE(NSTEP)
      IF (H.GT.0.D0) THEN
        IDIR=1
      ELSE
        IDIR=-1
      ENDIF
      R=REND
C
      DO 100 ISTEP=1,NSTEP+1
C  SET UP PARAMETERS FOR DOING SIMPSONS RULE INTEGRATION
        IF (ISTEP.EQ.1 .OR. ISTEP.EQ.NSTEP+1) THEN
          A=1D0
        ELSEIF (MOD(ISTEP,2).EQ.0) THEN
          A=4D0
        ELSE
          A=2D0
        ENDIF
C
C  WRITE OUT PSI TO WAVEFN SCRATCH FILE
C
        IF (IDIR.EQ.1 .OR. ISTEP.NE.1) WRITE(IPSISC,REC=IPREC) R,PSIA
C
        R=R-H
C  READ IN R(A,B) FROM WAVESCRATCH
        IF (ISTEP.EQ.NSTEP+1) GOTO 105
        IWREC=IWREC-1
        READ(IWAVSC,REC=IWREC,ERR=900) RP,RAB
        IF (ABS(RP-R).GT.1D-8) THEN
          WRITE(6,*)'SIGN OF ERROR??? RP = ',RP,' AND R = ',R
          STOP
        ENDIF
        IPREC=IPREC-IDIR
C  GENERATE PSI(B) FROM PSI(A) AND R(A,B)
        CALL DGEMUL(RAB,N,'N',PSIA,N,'N',PSIB,N,N,N,1)

 105    DO I=1,N
C  COLLECT TERMS FOR THE NORMALISATION CONSTANT
          SUMPSI(I)=SUMPSI(I)+A*PSIA(I)*PSIA(I)*ABS(H)/3.D0
C  COPY PSI(B) TO PSI(A) FOR NEXT SECTOR
          PSIA(I)=PSIB(I)
        ENDDO
        IF (IPRINT.GE.30) THEN
          WRITE(6,110) R,(PSIA(K),K=1,N)
 110      FORMAT(E12.5,7X,10(E12.5,5X))
        ENDIF
 100  CONTINUE
C
      RETURN
C
 900  WRITE(6,910) IWREC
 910  FORMAT('  *** ERROR - CRASHED ON READ OF RAB FILE - REC=',I6)
      END
