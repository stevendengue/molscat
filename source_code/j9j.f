      SUBROUTINE J9J(J1,J2,J4,J5,J6,J7,J8,J9,IVAL,J3MIN,D9J)
C  This subroutine is part of the MOLSCAT, BOUND and FIELD suite of programs
      IMPLICIT DOUBLE PRECISION (A-H,J-Z)
      DIMENSION D9J(1),D6J3(200),D6J5(200),D6J7(200)
      DATA MXDIM6/200/
      DATA ZERO/0.D0/,TENTH/0.1D0/,HALF/0.5D0/,ONE/1.D0/,TWO/2.D0/
C
C  THIS ROUTINE CALCULATES THE 9-J SYMBOLS {J1 J2 J3} BY SUMMATION OVER 6-J
C                                          {J4 J5 J6}
C                                          {J7 J8 J9}
C  SYMBOLS WHICH IN TURN ARE CALCULATED BY THE RECURSIVE METHOD
C  OF SCHULTEN AND GORDON.
C  PROGRAMMED BY D. E. FITZ, 22 OCT 79.
C
C  MODIFIED BY M. L. DUBERNET, 15 SEP 93 AND J. M. HUTSON, 3 OCT 93
C           TO ALLOW HALF-INTEGER ANGULAR MOMENTA
C  MODIFIED BY J. M. HUTSON, 3 OCT 93 TO CHECK D9J DIMENSION
C
      MXDIM9=IVAL
      J3MIN=MAX(ABS(J1-J2),ABS(J6-J9))
      J3MAX=MIN(J1+J2,J6+J9)
      IJ3N=INT(TWO*J3MIN+TENTH)
      IJ3X=INT(TWO*J3MAX+TENTH)
      IVAL=1+(IJ3X-IJ3N)/2
      IF (IVAL.GT.MXDIM9) THEN
        WRITE(6,*) ' J9J: ARRAY D9J TOO SMALL. NEEDS ',IVAL,
     1             ' BUT ONLY ',MXDIM9,' SUPPLIED'
        STOP
      ENDIF
C
C  TEST FOR TRIANGULAR INEQUALITIES.
C
      D9J(1)=ZERO
      IF (IVAL.LE.0) RETURN
      IJ1=INT(TWO*J1+TENTH)
      IJ2=INT(TWO*J2+TENTH)
      IJ4=INT(TWO*J4+TENTH)
      IJ5=INT(TWO*J5+TENTH)
      IJ6=INT(TWO*J6+TENTH)
      IJ7=INT(TWO*J7+TENTH)
      IJ8=INT(TWO*J8+TENTH)
      IJ9=INT(TWO*J9+TENTH)
      DO 15 IJL=1,IVAL
   15   D9J(IJL)=ZERO
      IF ((IJ4-ABS(IJ7-IJ1))*(IJ7+IJ1-IJ4).LT.0) RETURN
      IF ((IJ5-ABS(IJ8-IJ2))*(IJ8+IJ2-IJ5).LT.0) RETURN
      IF ((IJ5-ABS(IJ6-IJ4))*(IJ6+IJ4-IJ5).LT.0) RETURN
      IF ((IJ8-ABS(IJ9-IJ7))*(IJ9+IJ7-IJ8).LT.0) RETURN
C
      IVAL7=MXDIM6
      CALL J6J(J1,J9,J7,J8,J4,IVAL7,JMIN7,D6J7)
C
      IVAL5=MXDIM6
      CALL J6J(J6,J2,J5,J8,J4,IVAL5,JMIN5,D6J5)
C
      JMIN=MAX(JMIN5,JMIN7)
      JMAX=MIN(J1+J9,J2+J6,J4+J8)
      IEND=INT(JMAX-JMIN+TENTH+ONE)
      I5=INT(JMIN-JMIN5+TENTH)
      I7=INT(JMIN-JMIN7+TENTH)
C
C  LOOP RUNS OVER TWICE J3 TO ALLOW HALF-INTEGER VALUES
C
      ITAB=0
      DO 20 IJ3=IJ3N,IJ3X,2
        ITAB=ITAB+1
        J3=HALF*DBLE(IJ3)
C
        IVAL3=MXDIM6
        CALL J6J(J1,J9,J3,J6,J2,IVAL3,JMIN3,D6J3)
        I3=INT(JMIN-JMIN3+TENTH)
        TOTAL=ZERO
C
        DO 10 I=1,IEND
          J=DBLE(I-1)+JMIN
          SGN=ONE
          ISGN=INT(TWO*J+TENTH)
          IF (MOD(ISGN,2).NE.0) SGN=-ONE
          TOTAL=TOTAL+(TWO*J+ONE)*SGN*D6J3(I+I3)*D6J5(I+I5)*D6J7(I+I7)
   10   CONTINUE
        D9J(ITAB)=TOTAL
   20 CONTINUE
      RETURN
      END
