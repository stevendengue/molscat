      SUBROUTINE CNTRCT(N, M, R, EMAX, W, T,
     1                  VL, IV, EINT, CENT,
     2                  P, RMLMDA, MXLAM, NPOTL, IPRINT)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE potential
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  ROUTINE TO CONTRACT THE ANGULAR BASIS SET BY TRANSFORMING
C  THE VL ARRAY TO A REPRESENTATION THAT IS DIAGONAL AT R.
C
C  ON ENTRY: N IS SIZE OF ORIGINAL BASIS,
C            VL CONTAINS THE POTENTIAL MATRIX,
C            IV (IF USED) CONTAINS ADDRESSES OF POTENTIAL MATRIX ELEMENTS,
C            MXLAM IS MAXIMUM NUMBER OF TERMS IN POTENTIAL
C            IPRINT CONTROLS VERBOSITY OF OUTPUT
C  DURING:   W IS USED FOR THE HAMILTONIAN MATRIX
C            T IS USED FOR ITS EIGENVECTORS
C            EINT IS USED FOR THE THRESHOLDS,
C            CENT IS USED FOR THE CENTRIFUGAL TERMS,
C            P IS USED FOR THE CONSTANT TERMS IN THE POTENTIAL
C  ON EXIT:  M IS THE SIZE OF THE NEW BASIS
C            NPOTL IS MXLAM+2
C
      DIMENSION W(N,N),T(N,N),VL(2),IV(2),EINT(N),CENT(N),P(MXLAM)
      COMMON /MEMORY/ MX,IXNEXT,NIPR,IDUMMY,X(1)
      COMMON /VLSAVE/ IVLU
      COMMON /VLFLAG/ IVLFL
C
      DATA ZERO /0.D0/, HALF /0.5D0/, ONE /1.D0/
C
      IF (IVLFL.GT.0) THEN
        M=N
        WRITE(6,*) ' BASIS SET CONTRACTION NOT ALLOWED FOR IVLFL > 0'
        RETURN
      ENDIF
C
      IF (NCONST.GT.0 .OR. NRSQ.GT.0) THEN
        M=N
        WRITE(6,*) ' BASIS SET CONTRACTION NOT ALLOWED FOR NCONST > 0',
     1             ' OR NRSQ > 0'
        WRITE(6,*) ' BUT THIS MAY BE EASY TO FIX IN SUBROUTINE CNTRCT'
        RETURN
      ENDIF
C
      ISE=IXNEXT
      IXNEXT=ISE+N
      NUSED=1
      CALL CHKSTR(NUSED)
      ERED=0.D0
      NPOTL=MXLAM
      CALL WAVMAT(W,N,R,P,VL,IV,ERED,EINT,CENT,RMLMDA,X(ISD),
     1            MXLAM,NPOTL,IPRINT)
C
      IFAIL=0
      CALL DIAGVC(W,N,N,X(ISE),T)
C
      IF (IPRINT.GE.13) WRITE(6,602) EMAX,(X(ISE+I-1),I=1,N)
  602 FORMAT(/'  CONTRACTION WILL USE REDUCED ENERGY CUTOFF OF',
     1       F9.2/'  REDUCED EIGENVALUES OF ANGULAR HAMILTONIAN ARE'/
     2       (2X,9F8.2))
C
C  SCAN EIGENVALUES TO DECIDE HOW MANY CHANNELS TO KEEP
C
      DO 10 I=1,N
        M=I
        IF (X(ISE+I-1).GT.EMAX) GOTO 20
   10 CONTINUE
   20 CONTINUE
C
      IF (IPRINT.GE.3) WRITE(6,603) N,M,R
  603 FORMAT(/'  CONTRACTING BASIS SET FROM N =',I5,' TO M =',I4/
     1       '  USING FIXED-R EIGENVECTORS AT R =',F7.3)
      NPOTLC=NPOTL+2
C
      ISA=ISE
      ISV=ISA+N*M
      NVLC=M*(M+1)/2
      IXNEXT=ISV+NVLC*NPOTLC
      CALL CHKSTR(NUSED)
C
      IF (IVLU.GT.0) REWIND IVLU
      DO 400 LL=1,NPOTL
        IF (IVLU.EQ.0) THEN
          IX=LL
          DO 100 I=1,N
          DO 100 J=1,I
            W(J,I)=VL(IX)
            IX=IX+NPOTL
  100     CONTINUE
        ELSE
          READ(IVLU) ((W(J,I),J=1,I),I=1,N)
        ENDIF
C
        CALL DSYMM('L','U',N,M,ONE,W,N,T,N,ZERO,X(ISA),N)
        CALL DSYR2K('U','T',M,N,HALF,X(ISA),N,T,N,ZERO,W,N)
C
        IX=ISV+LL-1
        DO 200 I=1,M
        DO 200 J=1,I
          X(IX)=W(J,I)
  200     IX=IX+NPOTLC
  400 CONTINUE
C
      IF (IVLU.GE.10) CLOSE(IVLU)
C
      IX=ISV+NPOTL
      DO 600 I=1,M
      DO 600 J=1,I
        X(IX)=0.D0
        DO 500 K=1,N
  500     X(IX)=X(IX)+EINT(K)/RMLMDA*T(K,I)*T(K,J)
        W(I,J)=X(IX)
  600   IX=IX+NPOTLC
C
      IX=ISV+NPOTL+1
      DO 800 I=1,M
      DO 800 J=1,I
        X(IX)=0.D0
        DO 700 K=1,N
  700     X(IX)=X(IX)+CENT(K)*T(K,I)*T(K,J)
  800   IX=IX+NPOTLC
C
C  MONITOR HOW WELL THE CONTRACTED BASIS GIVES THE MONOMER LEVELS
C  NOTE THAT THE VL ARRAY CONTAINS EINT INFORMATION IN EXTERNAL
C  UNITS, NOT IN REDUCED UNITS AS IN MOLSCAT V14
C
      IF (IPRINT.GE.7) THEN
        CALL DIAGVL(W,N,M,X(ISA))
        WRITE(6,604) (X(ISA+I),I=0,6)
  604   FORMAT('  LOWEST EIGENVALUES OF E(INTERNAL) IN CONTRACTED',
     1         ' BASIS SET ARE'/(1X,7F11.4))
      ENDIF
C
      NCONST=1
      NRSQ=1
      VCONST(1)=1.D0
      IVLU=0
      NPOTL=NPOTLC
C
C  IT IS POSSIBLE FOR THE LENGTH OF THE CONTRACTED VL ARRAY TO
C  EXCEED THE OFFSET BETWEEN IT AND VL. THE COPY IS BROKEN UP INTO
C  SEGMENTS SMALLER THAN M*M TO AVOID ANY DANGER OF OVERWRITING.
C
C     NVLC=NPOTL*M*(M+1)/2
C     CALL DCOPY(NVLC,X(ISV),1,VL,1)
C
      DO 900 I=1,NPOTL
        CALL DCOPY(NVLC,X(ISV+NVLC*(I-1)),1,VL(1+NVLC*(I-1)),1)
  900 CONTINUE
C
      RETURN
      END
