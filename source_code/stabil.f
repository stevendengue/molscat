      SUBROUTINE STABIL(N,NB,Y,YP,F1,F2,
     1                  SCR,YN,YPN,F1N,F2N)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  FIND SCR SUCH THAT Y*SCR IS (PERMUTED) UNIT MATRIX,
C  THEN TRANSFORM Y, YP, F1, AND F2 BY RIGHT MULTIPLICATION WITH SCR.
C
C  ON ENTRY: Y,YP,F1,F2 ARE ARRAYS DIMENSIONED (N,N);
C            NB IS INTEGER ARRAY DIMENSIONED (N) - POSITION OF (PERMUTED)
C            DIAGONAL ELEMENTS;
C            YN,YPN,F1N,F2N ARE SCRATCH ARRAYS DIMENSIONED (N,N).
C  ON EXIT:  YP,F1,F2 ARE TRANSFORMED ARRAYS;
C            Y IS (PERMUTED) UNIT MATRIX;
C            SCR IS TRANSFORMATION MATRIX.
      DIMENSION NB(N),Y(1),YP(1),F1(1),F2(1),
     &          YN(1),YPN(1),F1N(1),F2N(1),SCR(1)
C
C  SAVE OLD INPUT MATRICES AND INITIALIZE SCR TO BE (PERMUTED) UNIT MATRIX
C
      NSQ=N*N
      CALL DCOPY(NSQ,Y,1,YN,1)
      CALL DCOPY(NSQ,YP,1,YPN,1)
      CALL DCOPY(NSQ,F1,1,F1N,1)
      CALL DCOPY(NSQ,F2,1,F2N,1)
      DO 1100 IJ=1,NSQ
        Y(IJ)=0.D0
 1100   SCR(IJ)=0.D0
      DO 1200 I=1,N
        IJ=N*(I-1)+NB(I)
        Y(IJ)=1.D0
 1200   SCR(IJ)=1.D0
C
C  CALCULATE SOLUTION TO YN*SCR=(PERMUTED) UNIT MATRIX
      CALL DGESV(N,N,YN,N,YP,SCR,N,IER)
      IF (IER.EQ.0) GOTO 2000
      WRITE(6,600)
  600 FORMAT('  * * * WARNING - STABILIZATION WITH BAD MATRIX.')
C
 2000 CALL DGEMUL(YPN,N,'N',SCR,N,'N',YP,N,
     1            N,N,N)
      CALL DGEMUL(F1N,N,'N',SCR,N,'N',F1,N,
     1            N,N,N)
      CALL DGEMUL(F2N,N,'N',SCR,N,'N',F2,N,
     1            N,N,N)
C
      RETURN
      END
