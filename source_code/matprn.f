      SUBROUTINE MATPRN(IUNIT,X,NDIM,N,NC,ISY,X2,A,MDIM)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  USAGE OF PARAMETERS :
C  IUNIT : FORTRAN CHANNEL NUMBER
C  X     : MATRIX FOR PRINTING IF IN 2-D ARRAY
C  NDIM  : FIRST DIM. OF X AS DECLARED IN CALLING PROCEDURE
C  N     : NO. OF ROWS (I.E. FIRST DIM.) TO BE PRINTED
C  NC    : NO. OF COLUMNS (I.E. SECOND DIM.) TO BE PRINTED
C  ISY   : SEE BELOW
C  X2    : MATRIX FOR PRINTING IF IN SYMMETRIC STORAGE MODE
C  A     : *-CHARACTER TITLE
C  MDIM  : SIZE OF ARRAY NECESSARY FOR STORING X2
C
C      USE OF ISYM---
C
C            MATRIX              OUTPUT
C
C  ISYM=0  SYMMETRIC           F FORMAT
C  ISYM=1  FULL                F FORMAT
C  ISYM=2  SYMMETRIC           E FORMAT
C  ISYM=3  FULL                E FORMAT
C  ISYM=4  SYM STORAGE         F FORMAT
C  ISYM=5  SYM STORAGE         E FORMAT
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION X(NDIM,NC),X2(MDIM)
      CHARACTER*(*) A

      ISYM=ISY
      ITIME=(NC+9)/10
      KF=0
      KF1=KF
      ISTOR=0
      IFMT=20
      IF (ISYM.LE.3) GOTO 4
      ISTOR=1
      IF (ISYM.EQ.5) IFMT=21
      ISYM=0
      GOTO 3
    4 IF (ISYM.LE.1) GOTO 3
      ISYM=ISYM-2
      IFMT=21
    3 CONTINUE
      WRITE(IUNIT,16)
      J1=1
      DO 100 KK=1,ITIME
C  PRINT HEADING AND COLUMN NUMBERS
        KS=(KK-1)*10 + 1
        KF=KS + 9
        KF1=KF
        IF (KK.EQ.ITIME) KF1=MIN(NC,KF)
        WRITE(IUNIT,*) A
        WRITE(IUNIT,11) (I,I=KS,KF1)
        IF (ISYM.EQ.0) J1=KS
        DO 200 JJ=J1,N
C  SYMMETRIC PRINTING
          IF (ISYM.EQ.0) KF1=MIN(JJ,KF)
          IF (ISTOR.EQ.1) GOTO 199
          IF (IFMT.EQ.20) WRITE(IUNIT,20) JJ,(X(JJ,I),I=KS,KF1)
          IF (IFMT.EQ.21) WRITE(IUNIT,21) JJ,(X(JJ,I),I=KS,KF1)
          GOTO 200
C  PRINTING FOR MATRICES IN SYM STORAGE MODE
  199     II1=JJ*(JJ-1)/2
          IF (IFMT.EQ.20) WRITE(IUNIT,20) JJ,(X2(II1+I),I=KS,KF1)
          IF (IFMT.EQ.21) WRITE(IUNIT,21) JJ,(X2(II1+I),I=KS,KF1)
  200   CONTINUE
        WRITE(IUNIT,16)
  100 CONTINUE
      RETURN
C  FORMATS MODIFIED BY JA ON 01/12/2008
C  ORIGINAL FORMATS:
C  11 FORMAT(/10(10X,I2))
C  20 FORMAT(' ',I2,10(1X,F11.5))
C  21 FORMAT(' ',I2,10E12.5)
C  NEW FORMATS:
   11 FORMAT(/3X,10(14X,I3))
   20 FORMAT(' ',I5,10(1X,F16.10))
   21 FORMAT(' ',I5,10(1X,E16.9))
C************************************
   16 FORMAT(//)
      END
