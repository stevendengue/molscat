      SUBROUTINE DMSYM(J,NK,EVAL,EVEC,EVEC2,A,B,C)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  THIS SUBROUTINE IS CALLED BY ASROT TO SYMMETRISE NUMERICALLY
C  DEGENERATE FUNCTIONS FOR ASYMMETRIC OR SPHERICAL TOPS.
C
C  FOR NEAR-SYMMETRIC TOPS, EVEN AND ODD COMBINATIONS OF FUNCTIONS
C  WITH HIGH K CAN BE VERY CLOSE TOGETHER.
C
C  THIS ROUTINE SETS UP SYMMETRIZED COMBINATIONS THAT ARE EVEN/ODD WRT K
C  BY CONSTRUCTING AND DIAGONALISING THE MATRIX REPRESENTATION OF SIGMA(XZ)
C  FOR EACH DEGENERATE SET.
C
C  IN SOME CASES THERE CAN BE DEGENERACIES INVOLVING EVEN AND ODD K VALUES TOO.
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      SAVE SYMPRT
      PARAMETER (IDMAX=3)
      LOGICAL SYMPRT
      CHARACTER(1) CSUB,REPNAM(3)
      DIMENSION SMAT(IDMAX,IDMAX),SVEC(IDMAX,IDMAX),SIG(IDMAX),
     1          EVAL2(IDMAX),EVEC2(-J:J,NK)
      DIMENSION EVAL(NK),EVEC(-J:J,NK)
      DATA SYMPRT/.FALSE./
      DATA REPNAM/'A','E','F'/
      DATA TOL/1.D-9/
C
      IMAX=0
      DO 1000 IC=1,NK
        IF (IC.LE.IMAX) GOTO 1000
C
C  LOOK FOR DEGENERATE EIGENVECTORS
C
        DO 200 JC=IC,NK
          IF (ABS(EVAL(JC)-EVAL(IC)).GT.TOL) GOTO 200
          IMAX=JC
  200   CONTINUE
C
        IDEG=1+IMAX-IC
        IF (IDEG.GT.IDMAX) GOTO 1100
        IF (IDEG.EQ.1) GOTO 920
        IF (IDEG.GE.3) SYMPRT=.TRUE.
C
C  NOW CONSTRUCT THE MATRIX REPRESENTATION OF SIGMA(XZ)
C
        DO 400 L=1,IDEG
          LC=IC+L-1
        DO 400 M=1,IDEG
          MC=IC+M-1
          SMAT(M,L)=0.D0
        DO 400 K=-J,J
          SMAT(M,L)=SMAT(M,L)+EVEC(K,MC)*EVEC(-K,LC)
  400   CONTINUE
C
        IFAIL=0
        CALL DIAGVC(SMAT,IDMAX,IDEG,SIG,SVEC)
C
C  COPY OLD EIGENVALUES AND EIGENVECTORS INTO EVAL2 AND EVEC2
C
        DO 500 L=1,IDEG
          LC=IC+L-1
          EVAL2(L)=EVAL(LC)
        DO 500 K=-J,J
          EVEC2(K,L)=EVEC(K,LC)
  500   CONTINUE
C
C CONSTRUCT NEW EIGENVECTORS
C
        DO 600 L=1,IDEG
          LC=IC+L-1
        DO 600 K=-J,J
          EVEC(K,LC)=0.D0
        DO 600 M=1,IDEG
          EVEC(K,LC)=EVEC(K,LC)+SVEC(M,L)*EVEC2(K,M)
          IF (ABS(EVEC(K,LC)).GT.0.5D0) KMAX=K
  600   CONTINUE
C
C CONSTRUCT NEW EIGENVALUES
C
      DO 620 L=1,IDEG
        LC=IC+L-1
        EVAL(LC)=0.D0
        DO 610 M=1,IDEG
           MC=IC+M-1
           EVAL(LC)=EVAL(LC)+EVAL2(M)*SVEC(M,L)**2
  610   CONTINUE
  620 CONTINUE
C
C  KMAX IS DOMINANT K. FOR NEAR-SYM TOP WITH SIMILAR A AND B,
C  IF K EVEN AND A+B > 2C, SIG=-1 IS ABOVE SIG=+1
C  IF K ODD  AND   B >   , SIG=-1 IS ABOVE SIG=+1
C  IN THESE CASES EXCHANGE NEW LEVELS SO THAT TAU LABELLING IS RIGHT
C
      IF ((MOD(KMAX,2).EQ.0 .AND. A+B.GT.C+C) .OR.
     1    (MOD(KMAX,2).EQ.1 .AND.   B.GT.A)) THEN
        TEMP=EVAL(IC)
        EVAL(IC)=EVAL(IC+1)
        EVAL(IC+1)=TEMP
        DO 650 K=-J,J
          TEMP=EVEC(K,IC)
          EVEC(K,IC)=EVEC(K,IC+1)
          EVEC(K,IC+1)=TEMP
  650   CONTINUE
      ENDIF
C
C  THERE IS STILL A POSSIBILITY THAT EVEN AND ODD K ARE MIXED,
C  BUT ONLY FOR TWO ADJACENT EIGENVECTORS. CHECK FOR THIS
C  AND FIX IT IF IT IS FOUND
C
        DO 900 L=1,IDEG-1
          LC=IC+L-1
          DO 700 K=-J,J-1
            IF (ABS(EVEC(K,LC)*EVEC(K+1,LC)).LT.TOL) GOTO 700
            THETA=ATAN2(EVEC(K,LC),EVEC(K,LC+1))
            CO=COS(THETA)
            SI=SIN(THETA)
            GOTO 800
  700     CONTINUE
          GOTO 900

  800     CONTINUE
C
C  ARRIVE HERE IF THERE IS MIXING OF ODD AND EVEN K
C
          DO 850 K=-J,J
            TEMP      =CO*EVEC(K,LC)-SI*EVEC(K,LC+1)
            EVEC(K,LC)=SI*EVEC(K,LC)+CO*EVEC(K,LC+1)
            EVEC(K,LC+1)=TEMP
  850     CONTINUE
  900   CONTINUE
C
C  SPECIAL CODE TO WORK OUT SYMMETRY LABEL FOR SPHERICAL TOP
C
  920   IF (.NOT.SYMPRT) GOTO 1000
        CSUB=' '
        IF (IDEG.EQ.2) GOTO 950
        CSUB='1'
        IF (J.LT.2) GOTO 950
        DO 940 L=1,IDEG
          LC=IC+L-1
          IF (EVEC(2,LC)**2.GT.TOL) CSUB='2'
  940   CONTINUE
C
  950   WRITE(6,601) EVAL(LC),REPNAM(IDEG),CSUB
  601   FORMAT('  ENERGY LEVEL AT',F12.5,' HAS SYMMETRY ',2A1)
C
 1000 CONTINUE

      RETURN
C
 1100 WRITE(6,699) IDEG,IDMAX
      STOP
  699 FORMAT(/'  *** ERROR IN DMSYM: DEGENERACY',I3,' IS TOO LARGE ',
     1        'FOR DIMENSION IDMAX =',I3)
      END
