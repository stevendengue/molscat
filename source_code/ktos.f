      SUBROUTINE KTOS(R,SR,SI,NOP)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION R(1),SR(1),SI(1)
C
C  ROUTINE TO OBTAIN THE S MATRIX FROM THE REACTANCE (K) MATRIX.
C  ALTHOUGH THIS ROUTINE USES SYMMETRIC MATRIX MULTIPLICATION,
C  THE WHOLE OF R MUST BE SUPPLIED AND THE WHOLE OF SR AND
C  SI ARE RETURNED.
C
C  I + R*R  IS POSITIVE DEFINITE, SO SYMINV CANNOT FAIL
C
C  ON ENTRY: R IS THE K MATRIX, DIMENSION (NOP,NOP), UNCHANGED ON EXIT.
C  ON EXIT:  SR AND SI ARE THE REAL AND IMAGINARY PARTS OF THE S MATRIX.
C
C  (SEE JOHNSON, JOURNAL OF COMPUTATIONAL PHYSICS 13, 445 (1973)
C   S=(I+SQRT(-1)K)^{-1}(I-SQRT(-1)K))
C
      NOPP1=NOP+1
      NOPSQ=NOP*NOP
C  SR=0.5*R*R
      CALL DSYMM('L','L',NOP,NOP,0.5D0,R,NOP,R,NOP,0.D0,SR,NOP)

C  SR=0.5*(R*R+I)
      DO 10 II=1,NOPSQ,NOPP1
   10   SR(II)=SR(II)+0.5D0

C  UPPER TRIANGLE OF SR NOW CONTAINS INVERSE OF 0.5*(R*R+I)
      CALL SYMINV(SR,NOP,NOP,IFAIL)
C  COPY TO LOWER TRIANGLE
      CALL DSYFIL('U',NOP,SR,NOP)
C  SI=SR*R
      CALL DSYMM('L','L',NOP,NOP,1.D0,SR,NOP,R,NOP,0.D0,SI,NOP)

C  SR=SR-I
      DO 30 II=1,NOPSQ,NOPP1
   30   SR(II)=SR(II)-1.D0
      RETURN
      END
