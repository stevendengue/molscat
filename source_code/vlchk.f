      SUBROUTINE VLCHK(IVLFL,IPRINT,ITYPE,NLABV,MXLAM,NPOTL,LAM)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE potential
      IMPLICIT NONE
      INTEGER, INTENT(IN)    :: IVLFL,IPRINT,ITYPE,NLABV(9),LAM(1)
      INTEGER, INTENT(INOUT) :: MXLAM
      INTEGER, INTENT(OUT)   :: NPOTL
C
C  RENAMED FROM IVCHK BY CR Le Sueur 09-11-2018
C
C  CR Le Sueur 02-08-18: CHANGED TO ALSO ASSIGN VALUES TO NPOTL/NVLBLK
C                        (CODE MOVED FROM POTENL)
C  CR Le Sueur/G McBane 09-10-18: REPLACED VERY SIMPLE CHECKING OF LAMBDA
C                                 ARRAY WITH MORE SOPHISTICATED ONE WRITTEN
C                                 BY G McBane
C
C  THIS ROUTINE CHECKS LAMBDA (MOLSCAT POTENTIAL SYMMETRY INDICES)
C  TO ASCERTAIN WHETHER IV() INDEXING SCHEME ('NON-TRIVIAL' CASES)
C  WILL WORK.  IMPLEMENTATION BEGINNING V9 WILL **NOT** WORK
C  PROPERLY IF TWO OF THE 'SYMMETRIES' ARE IDENTICAL.
C
C  ON ENTRY: IVLFL INDICATES WHETHER TO CHECK LAM ARRAY;
C            IPRINT IS INTEGER CONTROLLING VERBOSITY OF OUTPUT;
C            ITYPE IS INTEGER CONTROLLING WHAT TYPE OF SYSTEM IS UNDER
C            STUDY;
C            NLABV IS INTEGER CONTROLLING HOW MANY LABELS ARE NEEDED FOR
C            THIS ITYPE;
C            MXLAM IS INTEGER CONTROLLING HOW MANY TERMS IN
C            HAMILTONIAN;
C            NPOTL IS NOT USED;
C            LAM IS INTEGER ARRAY OF SYMMETRY LABELS DIMENSIONED
C            (MXLAM,NLABV(ITYPE)).
C  ON EXIT:  NPOTL IS THE NUMBER OF BLOCKS OF THE VL ARRAY USED FOR THE
C            INTERACTION POTENTIAL
      LOGICAL OKAY,LTEST,LUNSPC
      INTEGER ITP,IADD,IDPCHK,IEXTRA,NRPOTL,MAXL,NQ,NDUP,I
C  COMMON BLOCK FOR COMMUNICATING WITH SURBAS
      COMMON /NPOT  / NPTL
      INTEGER NPTL
C
C  CALLED FROM DRIVER AFTER BASIN(IOSBIN)/POTENL(INITIZATION)
C
      ITP=MOD(ITYPE,10)
      IADD=ITYPE-ITP

      IF (ITP.EQ.0) THEN
        WRITE(6,*) ' *** IVCHK.  ILLEGAL ITYPE',ITYPE
        STOP
      ENDIF

C  COUNT NUMBER OF BLOCKS OF VL ARRAY NEEDED FOR EXTRA OPERATORS
      NEXBLK=0
      IF (ITP.EQ.9 .AND. NEXTRA.GT.0) THEN
        LUNSPC=.TRUE.
        DO IEXTRA=1,NEXTRA
          IF (NEXTMS(IEXTRA).EQ.0) THEN
            IF (.NOT.LUNSPC) THEN
              WRITE(6,*) ' EXTRA OPERATOR',IEXTRA,'APPEARS TO',
     1                   ' USE NO BLOCKS OF COUPLING MATRIX'
              WRITE(6,*) ' FIX AND RECOMPILE'
              STOP
            ENDIF
          ELSE
            LUNSPC=.FALSE.
            NEXBLK=NEXBLK+NEXTMS(IEXTRA)
          ENDIF
        ENDDO

        IF (LUNSPC) THEN
          DO IEXTRA=1,NEXTRA
            NEXTMS(IEXTRA)=1
          ENDDO
          NEXBLK=NEXTRA
        ENDIF
      ENDIF

      IF (IVLFL.LE.0) THEN
        NRPOTL=MXLAM

      ELSE

        IF (ITP.EQ.2 .OR. ITP.EQ.7) THEN
          MAXL=0
          DO I=1,MXLAM
            MAXL=MAX(MAXL,LAM(I))
          ENDDO
          NRPOTL=MAXL+1
        ELSEIF (IADD.EQ.100) THEN
          NRPOTL=1
        ELSEIF (ITP.EQ.8) THEN
          NRPOTL=NPTL
          WRITE(6,9380) NRPOTL
 9380     FORMAT('  SURFACE FOURIER COMPONENTS'/'  NRPOTL =',I2,
     1           ' FROM SURBAS')
        ELSE
          NRPOTL=MXLAM
        ENDIF
        IF (MXLAM.LE.1) THEN
          NRPOTL=1
          MXLAM=ABS(MXLAM)
          RETURN
        ENDIF

        NQ=NLABV(ITP)

        NDUP = IDPCHK(NQ,MXLAM,LAM)
        IF (NDUP.NE.0) THEN
          WRITE(6,*) ' *** ERROR *** - FOUND ',NDUP,
     1               ' DUPLICATE SYMMETRIES IN LAMBDA'
          WRITE(6,*) '               IVCHK TERMINATING'
          STOP
        ENDIF
        IF (IPRINT.GE.3) WRITE(6,*) ' IVCHK.  COMPLETED SUCCESSFULLY.'

      ENDIF

      NPOTL=NRPOTL+NCONST+NRSQ

      NVLBLK=NPOTL+NEXBLK

      IF (IPRINT.GE.1) THEN
        WRITE(6,9980) NRPOTL
 9980   FORMAT('  INTERACTION MATRIX USES ',I3,' BLOCKS OF VL ARRAY',
     1         ' FOR R-DEPENDENT TERMS IN POTENTIAL')
        IF (NCONST.GT.0) WRITE(6,9981) NCONST,
     1                                 'CONSTANT    TERMS IN POTENTIAL'
        IF (NRSQ.GT.0)   WRITE(6,9981) NRSQ,'CENTRIFUGAL OPERATOR'
        IF (NEXTRA.GT.0) WRITE(6,9982) NEXBLK,NEXTRA
        IF (NVLBLK.NE.NRPOTL) WRITE(6,9983) NVLBLK
 9981   FORMAT(26X,I3,20X,'FOR ',A)
 9982   FORMAT(26X,I3,20X,'FOR ',I3,' EXTRA OPERATORS')
 9983   FORMAT(/'  IN TOTAL:',I3,' BLOCKS OF VL ARRAY ARE USED'/)
      ENDIF

      RETURN
      END
