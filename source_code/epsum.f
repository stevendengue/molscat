      FUNCTION EPSUM(R,N,E)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  FUNCTION TO EVALUATE THE EIGENPHASE SUM FROM THE R-MATRIX.
C  DIAGVL DIAGONALISES THE N X N REAL SYMMETRIC R-MATRIX,
C  RETURNING THE EIGENVALUES IN E.
C  THE EIGENPHASE SUM IS THEN OBTAINED BY SUMMING ARCTANGENTS
C  OF THE EIGENVALUES.
C  THE RESULT IS RETURNED IN UNITS OF PI, AND IS SHIFTED TO BE
C  AS CLOSE AS POSSIBLE TO THE PREVIOUS EIGENPHASE SUM CALCULATED
C  (STORED IN SMLAST)
C  SEE ASHTON, CHILD AND HUTSON, J. CHEM. PHYS. 78, 4025 (1983).
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      SAVE SMLAST
      DIMENSION R(N,N), E(N)
      DATA PI/3.141592653589793238462643D0/
      DATA SMLAST/10.D0/
C
      IF (N.EQ.1) GOTO 200
      IFAIL=0
      CALL DIAGVL(R,N,N,E)
C
      EPSUM=0.D0
      DO 100 I=1,N
        X=ATAN(E(I))
        EPSUM=EPSUM+X
  100 CONTINUE
C
      GOTO 300
  200 EPSUM=ATAN(R(1,1))
  300 EPSUM=EPSUM/PI
      DELTA=SMLAST-EPSUM+0.5D0

      IF (DELTA.LE.0.D0) DELTA=DELTA-1.D0
      IDEL=INT(DELTA)
      EPSUM=EPSUM+DBLE(IDEL)
      SMLAST=EPSUM
      RETURN
      END
