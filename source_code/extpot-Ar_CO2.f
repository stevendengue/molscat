      FUNCTION EXTPOT(R,COSTH)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE physical_constants
C
C  SUBROUTINE TO EVALUATE SYSTEMATIC FITTING POTENTIAL WITH
C  OVERLAP-BASED REPULSION ENERGY
C  FOR RARE GAS - CO2 SYSTEMS
C
C  VERSION OF MAY 1995
C
C  FUNCTION BY J. M. HUTSON, DEPARTMENT OF CHEMISTRY,
C  UNIVERSITY OF DURHAM, DURHAM, DH1 3LE, ENGLAND.
C  ELECTRONIC MAIL:   J.M.HUTSON @ DURHAM.AC.UK
C
C  THIS FUNCTION EVALUATES THE RG-CO2 POTENTIAL FOR A
C  SINGLE VALUE OF THE INTERMOLECULAR DISTANCE (R) AND
C  COSINE THETA (COSTH).
C  R MUST BE SUPPLIED IN ANGSTROMS AND THE RESULT IS RETURNED
C  IN CM-1, BUT INTERNAL WORKINGS ARE IN ATOMIC UNITS.
C
C  SUBROUTINE EXTINT MUST BE CALLED BEFORE EXTPOT (ONCE IN EACH
C  PROGRAM RUN, OR FOR EACH NEW SET OF POTENTIAL PARAMETERS)
C  TO READ THE PARAMETERS AND PROCESS THEM.
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      SAVE
C
      DIMENSION RLAM(20),BLAM(20),OLAM(20)
C
      DATA RLAM/20*0.D0/, BLAM/20*0.D0/
C     DATA AUE/219474.6354D0/
C     DATA AUR/0.52917706D0/
C
C  20-09-2016: UPDATED TO USE MODULE (physical_constants) THAT CONTAINS
C              CONSISTENT AND UP-TO-DATE VALUES
      AUE=hartree_in_inv_cm
      AUR=bohr_to_Angstrom
C
C  CHECK THAT THE LONG-RANGE PARAMETERS HAVE BEEN INITIALISED.
C
      IF (RLAM(1) .LE. 0.D0) GOTO 998
C
C  FIND DISTANCES AND ANGLES TO THE DISPERSION SITES
C
      RR=R/AUR
C
C  TWO OPTIONS FOR THE OVERLAP FUNCTION:
C  SITE-SITE OR CENTRAL EXPANSION.
C
C  SITE-SITE OVERLAP FIRST
C
      IF (ISITE.GT.0) THEN
C
        T=-BOND/RR
        TEMP=SQRT(1.D0+T*(T+COSTH+COSTH))
        RO1=RR*TEMP
        COSO1=(COSTH+T)/TEMP
C
        T=+BOND/RR
        TEMP=SQRT(1.D0+T*(T+COSTH+COSTH))
        RO2=RR*TEMP
        COSO2=(COSTH+T)/TEMP
C
        DO 6 I=1,NLEGC
    6   OLAM(I)=RLAM(I)*EXP(BFAC*BLAM(I)*(RREF-RR))
        OLAPC=SUMLEG(OLAM,NLEGC,COSTH)
        DO 7 I=1,NLEGO
    7   OLAM(I)=RLAM(NLEGC+I)*EXP(BFAC*BLAM(NLEGC+I)*(RREF-RO1))
        OLAPO1=SUMLEG(OLAM,NLEGO,COSO1)
        DO 8 I=1,NLEGO
    8   OLAM(I)=RLAM(NLEGC+I)*EXP(BFAC*BLAM(NLEGC+I)*(RREF-RO2))
        OLAPO2=SUMLEG(OLAM,NLEGO,-COSO2)
C       WRITE(6,*) 'RC  =',RR ,'; OVERLAP C  =',OLAPC
C       WRITE(6,*) 'RO1 =',RO1,'; OVERLAP O1 =',OLAPO1
C       WRITE(6,*) 'RO2 =',RO2,'; OVERLAP O2 =',OLAPO2
C       WRITE(6,*) 'TOTAL OVERLAP =',OLAPC+OLAPO1+OLAPO2
        V=SFACC*OLAPC+SFACE*(OLAPO1+OLAPO2)
C
C  CENTRAL EXPANSION WITH REFERENCE DISTANCE ETC. AS LEGENDRE SUM
C
      ELSE
        R0=SUMLEG(RLAM,NLEGR,COSTH)
        BETA=BFAC*SUMLEG(BLAM,NLEGB,COSTH)
        RD=R0-RR
        OLAP=AOLAP*EXP(RD*(BETA-RD*GAMMA))
C       WRITE(6,*) 'OVERLAP =',OLAP
        V=(SFAC0+SFAC2*(1.5D0*COSTH*COSTH-0.5D0))*OLAP
      ENDIF
C
C  NOW GET DISTANCES AND ANGLES TO DISPERSION SITES,
C  WHICH ARE NOT USUALLY AT THE ATOMS
C
      T=-SITE/RR
      TEMP=SQRT(1.D0+T*(T+COSTH+COSTH))
      RO1=RR*TEMP
      COSO1=(COSTH+T)/TEMP
C
      T=+SITE/RR
      TEMP=SQRT(1.D0+T*(T+COSTH+COSTH))
      RO2=RR*TEMP
      COSO2=(COSTH+T)/TEMP
C
C  DISPERSION TERM FOR CENTRAL SITE
C
      R2=1.D0/(RR*RR)
      DISP=-(C6C+(C8C+C10C*R2)*R2
     1   +(C6C2+(C8C2+C10C2*R2)*R2)*(1.5D0*COSTH*COSTH-0.5D0))*R2**3
      IF (RR.GT.RDISPC) THEN
        V=V+DISP
      ELSE
        V=V+DISP*EXP(-0.4D0*(RDISPC/RR-1.D0)**2)
      ENDIF
C
C  DISPERSION TERM FOR TERMINAL SITE 1
C
      R2=1.D0/(RO1*RO1)
      DISP=-(C6O+(C8O+C10O*R2)*R2
     1   +(C6O2+(C8O2+C10O2*R2)*R2)*(1.5D0*COSO1*COSO1-0.5D0))*R2**3
      IF (RO1.GT.RDISPO) THEN
        V=V+DISP
      ELSE
        V=V+DISP*EXP(-0.4D0*(RDISPO/RO1-1.D0)**2)
      ENDIF
C
C  DISPERSION TERM FOR TERMINAL SITE 2
C
      R2=1.D0/(RO2*RO2)
      DISP=-(C6O+(C8O+C10O*R2)*R2
     1   +(C6O2+(C8O2+C10O2*R2)*R2)*(1.5D0*COSO2*COSO2-0.5D0))*R2**3
      IF (RO2.GT.RDISPO) THEN
        V=V+DISP
      ELSE
        V=V+DISP*EXP(-0.4D0*(RDISPO/RO2-1.D0)**2)
      ENDIF
C
C  SCALE BACK TO CM-1
C
      EXTPOT=V*AUE
      RETURN
C
  998 WRITE(6,999)
  999 FORMAT('  *** ERROR IN SUBROUTINE EXTPOT.'/
     1       '      EXTINT MUST BE CALLED BEFORE EXTPOT TO READ '
     1       'PARAMETERS FOR LONG-RANGE POTENTIAL.')
      STOP
C============================================================================
      ENTRY EXTINT
C
C  READ LONG-RANGE PARAMETERS (IN ATOMIC UNITS)
C  AND PROCESS THEM A LITTLE.
C
      BFAC=1.D0
C
      READ(5,*) ISITE,SFAC0,SFAC2,BFAC
C
      IF (ISITE.GT.0) THEN
        READ(5,*) NLEGC,NLEGO,RREF,BOND
        READ(5,*) (RLAM(I),BLAM(I),I=1,NLEGC+NLEGO)
        DO 100 I=1,NLEGC+NLEGO
  100   RLAM(I)=RLAM(I)*1.D-6
      ELSE
        READ(5,*) AOLAP
        READ(5,*) NLEGR
        READ(5,*) (RLAM(I),I=1,NLEGR,2)
        READ(5,*) NLEGB
        READ(5,*) (BLAM(I),I=1,NLEGB,2)
        READ(5,*) GAMMA
      ENDIF
C
      READ(5,*) C6,C6AFAC
      READ(5,*) SITE,RDISP,FRACC
      READ(5,*) C8RAT,C8AFAC
C
      C6C=FRACC*C6
      C6O=0.5D0*(C6-C6C)
      FRACC2=FRACC
C
      C62=C6AFAC*C6
      C6C2=FRACC2*C62
      C6O2=0.5D0*(C62-C6C2)
C
      C10RAT=49.D0/40.D0
C
C  RATIO OF C8 TO C6 FOR H-H FROM KOIDE ET AL, CHEM PHYS 58, 105 (81)
C
      C8RATH=19.1412D0
C
      C8RATC=C8RAT
      C8C=C8RATC*C6C
      C10C=C10RAT*C8RATC*C8C
      C8C2=C8RATC*C6C2
      C10C2=C10RAT*C8RATC*C8C2
      RDISPC=RDISP
      IF (RDISP.LT.0.D0) RDISPC=10.01D0*SQRT(C8RATC/C8RATH)
C
      C8RATO=C8RAT
      C8O=C8RATO*C6O
      C10O=C10RAT*C8RATO*C8O
      C8O2=C8RATO*C6O2
      C8=C8C+C8O+C8O+SITE**2*(10.D0*C6O+(72.D0/35.D0)*C6O2)
C     C82=C8*C8AFAC
C     C8O2=C82-SITE**2*(32.D0*C6O+(86.D0/7.D0)*C6O2)
      C10O2=C10RAT*C8RATO*C8O2
      RDISPO=RDISP
      IF (RDISP.LT.0.D0) RDISPO=10.01D0*SQRT(C8RATO/C8RATH)
C
      WRITE(6,601) ' CARBON ATOM C_N(0)',C6C,C8C,C10C
      WRITE(6,601) ' CARBON ATOM C_N(2)',C6C2,C8C2,C10C2
      WRITE(6,601) ' OXYGEN ATOM C_N(0)',C6O,C8O,C10O
      WRITE(6,601) ' OXYGEN ATOM C_N(2)',C6O2,C8O2,C10O2
C
      C82=C8C+C8O+C8O+SITE**2*(32.D0*C6O+(86.D0/7.D0)*C6O2)
      WRITE(6,601) ' CENTRAL     C_N(0)',C6,C8,C8/C6
      WRITE(6,601) ' CENTRAL     C_N(2)',C62,C82,C82/C8
  601 FORMAT(1X,A,1X,F8.3,F10.1,F12.0)
C
      WRITE(6,602) SITE
  602 FORMAT(/'  DISPERSION SITES AT',F8.3,' A0 FROM CENTRE')
C
      IF (ISITE.GT.0) THEN
        WRITE(6,603) RREF,BOND,(I,1.D6*RLAM(I),BLAM(I),I=1,NLEGC+NLEGO)
  603   FORMAT(/'  SITE-SITE MODEL USED FOR OVERLAP WITH RREF =',F8.3,
     1         ', BOND LENGTH =',F8.3/(I4,'  A =',F8.3,'  BETA =',F8.4))
        SFACC=SFAC0+0.5D0*SFAC2
        SFACE=SFAC0-0.5D0*SFAC2
        WRITE(6,604) SFACC,SFACE
  604   FORMAT(/'  REPULSION ENERGY FACTOR ON C ATOM: ',F8.4
     1         /'  REPULSION ENERGY FACTOR ON O ATOMS:',F8.4)
      ELSE
        WRITE(6,605) 'LEGENDRE TERMS IN R0  ',(RLAM(I),I=1,NLEGR,2)
        WRITE(6,605) 'LEGENDRE TERMS IN BETA',(BLAM(I),I=1,NLEGB,2)
        WRITE(6,605) 'QUADRATIC EXPONENT    ',GAMMA
  605   FORMAT(2X,A22,8F9.4)
        WRITE(6,606) SFAC0,SFAC2
  606   FORMAT(/'  REPULSION ENERGY FACTOR IS',F8.4,' +',F8.4,
     1          '* P2(COS THETA)')
      ENDIF
C
      RETURN
      END
