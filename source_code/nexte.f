      SUBROUTINE NEXTE(E,EP,ENEXT,DNRG,EFACT,UNAME,KSAVE,IPRINT)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  GIVEN THE S-MATRIX EIGENPHASE SUMS EP(5) AT FIVE EQUALLY SPACED
C  ENERGIES E(5), ESTIMATE THE POSITION ENEXT OF THE NEAREST
C  RESONANCE. THE ROUTINE ASSUMES A LINEAR BACKGROUND FOR THE EP'S.
C  DIFFERENCES ARE USED TO ESTIMATE THREE SUCCESSIVE VALUES OF THE
C  SECOND DERIVATIVE OF EIGSUM W.R.T. ENERGY, AND THESE SECOND
C  DERIVATIVES ARE THUS ASSUMED TO ARISE ENTIRELY FROM THE DISTANT
C  RESONANCE. THE RESONANCE POSITION IS THEN ESTIMATED BY AN
C  EXTRAPOLATION, BASED ON AN APPROXIMATION TO THE BREIT-WIGNER
C  FORMULA VALID AT ENERGIES MUCH FURTHER FROM THE RESONANCE THAN
C  ITS WIDTH. IF ANY OF THE SECOND DERIVATIVES DIFFER IN SIGN,
C  OR DECREASE WITH INCREASING ENERGY, THEN EITHER THE ENERGIES
C  INVOLVED ARE NEAR-RESONANT OR NUMERICAL NOISE IS DOMINATING
C  THE SECOND DERIVATIVES. UNDER THESE CIRCUMSTANCES, THE DNRG
C  PARAMETER IS INCREASED BY A FACTOR OF 10 TO REDUCE NUMERICAL
C  NOISE.
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      SAVE IAMB
      CHARACTER(8) UNAME
      DIMENSION E(5),EP(5)
      DATA IAMB/0/

      CUBERT(X)=SIGN(ABS(X)**(1.D0/3.D0),X)

      IF (IPRINT.GE.6) THEN
        IF (EFACT.NE.1.D0) THEN
          WRITE(6,600) (I,E(I),E(I)/EFACT,UNAME,EP(I),I=1,5)
        ELSE
          WRITE(6,700) (I,E(I),EP(I),I=1,5)
        ENDIF
      ENDIF
  600 FORMAT(/'  ESTIMATE PARAMETERS OF DISTANT RESONANCE ',
     1       'USING 5 ENERGIES'//('  ENERGY(',I1,') =',G22.14,
     2       ' CM-1 =',G22.14,1X,A,' EPSUM/PI =',F19.15))
  700 FORMAT(/'  ESTIMATE PARAMETERS OF DISTANT RESONANCE ',
     1       'USING 5 ENERGIES'//('  ENERGY(',I1,') =',G22.14,
     2       ' CM-1 EPSUM/PI =',F19.15))

      DNRG=E(2)-E(1)
      CURV1=(EP(1)-2.D0*EP(2)+EP(3))/(DNRG*DNRG)
      CC1=CUBERT(CURV1)
      CURV2=(EP(2)-2.D0*EP(3)+EP(4))/(DNRG*DNRG)
      CC2=CUBERT(CURV2)
      CURV3=(EP(3)-2.D0*EP(4)+EP(5))/(DNRG*DNRG)
      CC3=CUBERT(CURV3)
      IF (IPRINT.GE.6) WRITE(6,601) CURV1,CURV2,CURV3
  601 FORMAT(/'  ESTIMATED EIGENPHASE CURVATURES ARE',3(1X,G13.6))
C
      CURVSM=CURV1+CURV2+CURV3
      IF (CURV1*CURV2.LT.0.D0) GOTO 100
      IF (CURV2.LT.CURV1) GOTO 100
      IF (CURV2*CURV3.LT.0.D0) GOTO 100
      IF (CURV3.LT.CURV2) GOTO 100

      IAMB=0
      ENEXT1=(CC1*E(2) - CC2*E(3))/(CC1-CC2)
      ENEXT2=(CC2*E(3) - CC3*E(4))/(CC2-CC3)
      ENEXT3=(CC1*E(2) - CC3*E(4))/(CC1-CC3)
      IF (IPRINT.GE.10) WRITE(6,602) ENEXT1/EFACT,ENEXT2/EFACT,
     1                               ENEXT3/EFACT,UNAME
  602 FORMAT(/'  ESTIMATES OF ENERGY ARE',3G22.14,1X,A)

      GAM1=ABS(CURV1*(E(2)-ENEXT1)**3)
      GAM2=ABS(CURV2*(E(3)-ENEXT2)**3)
      GAM3=ABS(CURV1*(E(2)-ENEXT3)**3)
      IF (IPRINT.GE.10) WRITE(6,603) GAM1/EFACT,GAM2/EFACT,
     1                               GAM3/EFACT,UNAME
  603 FORMAT('  ESTIMATES OF WIDTH  ARE',3(7X,G11.4,4X),1X,A)

      ENEXT=(ENEXT1+ENEXT2+ENEXT3)/3.D0
      GAM=(GAM1+GAM2+GAM3)/3.D0
      ESTEP=ABS(ENEXT-E(3))
      IF (IPRINT.GE.6) WRITE(6,604) ENEXT/EFACT,UNAME,GAM/EFACT,UNAME
  604 FORMAT(/'  ESTIMATE RESONANCE ENERGY =',G22.14,1X,A,
     1        '   WIDTH =',E11.4,1X,A)
      IF (KSAVE.GT.0) WRITE(KSAVE,610) CURV1,CURV2,CURV3,
     1                                 ENEXT/EFACT,UNAME,GAM/EFACT,UNAME
  610 FORMAT('  *** CURVATURES',3G22.14,/
     1       '  *** GIVE ERES=',G22.14,1X,A,'    GAM =',E11.4,1X,A)

      DNMIN=0.25D0*DNRG
      DNMAX=4.D0*DNRG
      DNRG=MAX(0.02D0*ESTEP, 0.3D0*GAM)
      DNRG=MAX(DNRG,DNMIN)
      DNRG=MIN(DNRG,DNMAX)
      ENEXT=ENEXT-2.D0*DNRG
      RETURN
C
100   IAMB=IAMB+1
      DNRG=DNRG*10.D0
      IF (IPRINT.GE.1) WRITE(6,697) DNRG/EFACT,UNAME
697   FORMAT(/'  *** NEXTE: EIGENPHASE CURVATURES AMBIGUOUS: EITHER',
     1       ' ENERGIES SPAN RESONANCE OR PHASES AFFECTED BY'
     2       ' NUMERICAL NOISE:'/13X,'UNSAFE TO ESTIMATE RESONANCE',
     3       ' ENERGY. DNRG INCREASED TO',G12.5,1X,A)
      IF (KSAVE.GT.0) WRITE(KSAVE,698) CURV1,CURV2,CURV3
698   FORMAT(' *** CURVATURES',3G18.10,' AMBIGUOUS')
      IF (IAMB.GE.3) GOTO 200
      IF (CURVSM.LT.0.D0) ENEXT=E(1)-DNRG*5.D0
      IF (CURVSM.GE.0.D0) ENEXT=E(5)+DNRG
      RETURN
C
200   ENEXT=-1.D0
      RETURN
      END
