      SUBROUTINE CHKSTR(NUSED)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  NEW ROUTINE FOR DYNAMIC STORAGE HANDLING (S Green: 1/21/93)
C  REVISED 2 FEB 94 TO REFLECT USAGE OF UPPER X() IN HIH2O
C
C  NUSED.LT.0 ON ENTRY RESETS HIH2O (HIGH-WATER MARK)
C                 ALSO RESETS MX=MAXMAX
C  HIH2O DOES NOT REFLECT USE OF THE TOP OF X()
C  NOTE: IT IS *NOT* NECESSARY TO PASS NUSED DOWN TO ALL ROUTINES
C        WHICH CALL CHKSTR.  HOWEVER, A NON-NEGATIVE VALUE MUST
C        BE SUPPLIED TO PREVENT RESETTING HIH2O, MX.
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER HIH2O
      SAVE HIH2O,MAXMAX
C
C  DYNAMIC STORAGE COMMON BLOCK ...
      COMMON /MEMORY/ MX,IXNEXT,NIPR,IDUMMY,X(1)
C
      DATA MAXMAX/0/
C
C  NEGATIVE INPUT NUSED IS SIGNAL TO RESET HIH2O, MX
      IF (NUSED.LT.0) THEN
        HIH2O=0
        MX=MAX(MX,MAXMAX)
      ENDIF
C
C  STORAGE REQUIREMENT FOR CURRENT CALL IS ITOP=IXNEXT-1
      ITOP=IXNEXT-1
      IF (ITOP.GT.MX) THEN
        WRITE(6,600) ITOP,MX,MAXMAX
  600   FORMAT(/'  CHKSTR.  CANNOT PROVIDE REQUESTED STORAGE.'/
     1          11X,'CURRENT REQUEST =',I12/
     2          11X,'CURRENT  LIMIT  =',I12/
     3          11X,'ORIGINAL LIMIT  =',I12)
        STOP
      ELSE
        MAXMAX=MAX(MAXMAX,MX)
        NEED=ITOP+(MAXMAX-MX)
        HIH2O=MAX(HIH2O,NEED)
        NUSED=HIH2O
      ENDIF
      RETURN
      END
