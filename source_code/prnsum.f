C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  This set of subroutines used to print out information relating to
C  the progress of a bound-state location
C================================================================ PRPROP
      SUBROUTINE PRPROP(SVNAME,SVVAL,SVUNIT)
C  PRINT PROPAGATION MESSAGE
      IMPLICIT NONE

      DOUBLE PRECISION, INTENT(IN) :: SVVAL
      CHARACTER(*),     INTENT(IN) :: SVNAME,SVUNIT
      CHARACTER(38) SVTEMP !73-35

      SVTEMP=SVNAME
      SVTEMP=ADJUSTR(SVTEMP)

      WRITE(6,100) SVTEMP,SVVAL,SVUNIT
  100 FORMAT(/2X,'COUPLED EQUATIONS PROPAGATED AT ',A,' = ',1PG17.10, !35+A1
     1       1X,A)

      RETURN
      END
C================================================================ PRPRCT
      SUBROUTINE PRPRCT(NPROP,SVNAME,SVVAL,SVUNIT)
C  PRINT PROPAGATION MESSAGE
      IMPLICIT NONE

      DOUBLE PRECISION, INTENT(IN) :: SVVAL
      CHARACTER(*),     INTENT(IN) :: SVNAME,SVUNIT
      INTEGER,          INTENT(IN) :: NPROP
      CHARACTER(53) SVTEMP !70-17
      INTEGER LENSV,I

      LENSV=LEN(TRIM(ADJUSTL(SVNAME)))
      SVTEMP=SVNAME
      SVTEMP=ADJUSTR(SVTEMP)
      DO I=1,52-LENSV,2
        SVTEMP(I:I)='-'
        SVTEMP(I+1:I+1)=' '
      ENDDO

      WRITE(6,100) NPROP,SVTEMP,SVVAL,SVUNIT
  100 FORMAT(/2X,'PROPAGATION ',I3,': ',A,' = ',1PG17.10,1X,A)
      RETURN
      END
C================================================================ PRNDCT
      SUBROUTINE PRNDCT(INODE,NSEEK)
C  PRINT NODE COUNT BEING SEARCHED FOR
      IMPLICIT NONE

      INTEGER, INTENT(IN) :: INODE,NSEEK

      WRITE(6,100) INODE,NSEEK
  100 FORMAT(/2X,59('- ')//'  SEARCHING FOR INODE =',I5,
     1       ' CORRESPONDING TO NODE COUNT =',I5)

      RETURN
      END
C================================================================ PRBIS
      SUBROUTINE PRBIS(SVMIN,SVMAX,SVBIS,SVUNIT,METHOD)
C  PRINT POINTS USED BY ROOT-FINDING METHOD AND NEW POINT
      IMPLICIT NONE

      DOUBLE PRECISION, INTENT(IN) :: SVMIN,SVMAX,SVBIS
      CHARACTER(*),     INTENT(IN) :: SVUNIT,METHOD

      IF (METHOD.EQ.'BISECTION') THEN
        WRITE(6,100) SVMIN,SVMAX,SVBIS,SVUNIT
      ELSE
        WRITE(6,200) SVMIN,SVMAX,SVBIS,SVUNIT
      ENDIF
  100 FORMAT(/2X,'BISECTION ON RANGE ',1PG17.10,' TO ',G17.10,
     1       ' GIVES',10X,G17.10,1X,A) !28+34
  200 FORMAT(/2X,'SECANT WITH POINTS ',1PG17.10,' AND ',G17.10,
     1       ' GIVES',9X,G17.10,1X,A) !28+34

      RETURN
      END
C================================================================ PRMNMX
      SUBROUTINE PRMNMX(NODLO,NODHI,SVMIN,SVMAX,SVNAME,SVUNIT)
C  PRINT NODE COUNT AT MINIMUM AND MAXIMUM VALUES OF VARIABLE
      IMPLICIT NONE

      INTEGER,          INTENT(IN) :: NODLO,NODHI
      DOUBLE PRECISION, INTENT(IN) :: SVMIN,SVMAX
      CHARACTER(*),     INTENT(IN) :: SVNAME,SVUNIT

      IF (SVMIN.NE.SVMAX) THEN
        WRITE(6,100) NODLO,TRIM(SVNAME),SVMIN,SVUNIT,
     1               NODHI,TRIM(SVNAME),SVMAX,SVUNIT
        IF (NODHI.EQ.NODLO) THEN
          IF (SVNAME.EQ.'ENERGY') THEN
            WRITE(6,200) TRIM(SVNAME),'EFV SET'
          ELSE
            WRITE(6,200) TRIM(SVNAME),'ENERGY'
          ENDIF
        ENDIF
      ELSE
        WRITE(6,100) NODLO,TRIM(SVNAME),SVMIN,SVUNIT
      ENDIF
  100 FORMAT(/2X,'NODE COUNT =',I5,' AT MINIMUM ',A,' = ',
     1       1PG17.10,1X,A:/
     2       2X,'NODE COUNT =',0P,I5,' AT MAXIMUM ',A,' = ',
     3       1PG17.10,1X,A)
  200 FORMAT(/'  NO NODES BETWEEN MINIMUM AND MAXIMUM ',A,'.'/
     1               '  CALCULATION WILL BE SKIPPED FOR THIS JTOT, ',
     2               'SYMMETRY BLOCK AND ',A)

      RETURN
      END
C================================================================ PREVAL
      SUBROUTINE PREVAL(NODE,EIGMIN)
C  PRINT NODE COUNT AND SMALLEST EIGENVALUE
      IMPLICIT NONE

      INTEGER,          INTENT(IN) :: NODE
      DOUBLE PRECISION, INTENT(IN) :: EIGMIN
      WRITE(6,100) NODE,EIGMIN
  100 FORMAT(2X,'NODE COUNT IS ',I5,' AND SMALLEST EIGENVALUE OF ',
     1       'MATCHING MATRIX IS ',1PG12.5)

      RETURN
      END
C================================================================ PRABSE
      SUBROUTINE PRABSE(E,EREF,EFACT,EUNIT)
C  PRINT ABSOLUTE VALUE OF ENERGY
      IMPLICIT NONE

      DOUBLE PRECISION, INTENT(IN) :: E,EREF,EFACT
      CHARACTER(*),     INTENT(IN) :: EUNIT

      IF (EREF.NE.0.D0 .AND. EFACT.EQ.1.D0) THEN
        WRITE(6,100) E
      ELSEIF (EREF.NE.0.D0) THEN
        WRITE(6,200) E,E/EFACT,EUNIT
      ENDIF
  100 FORMAT(2X,1P,'WHICH IS EQUIVALENT TO AN ABSOLUTE ENERGY OF ', !45+28
     3                             28X,G17.10,1X,'CM-1')
  200 FORMAT(2X,1P,'WHICH IS EQUIVALENT TO AN ABSOLUTE ENERGY OF ',
     3       G17.10,1X,'CM-1',3X,' = ',G17.10,1X,A)

      RETURN
      END
C================================================================ PRBEND
      SUBROUTINE PRBEND(ALGNAM)
C  PRINT NAME OF ALGORITHM USED
      IMPLICIT NONE

      CHARACTER(*), INTENT(IN) :: ALGNAM

      WRITE(6,100) TRIM(ALGNAM)
  100 FORMAT(/2X,'BISECTION TO FIND SINGLE-NODE RANGE FINISHED.',
     1        2X,'USING ',A,' TO GENERATE FUTURE POINTS.')

      RETURN
      END
C================================================================ PRVWST
      SUBROUTINE PRVWST(XA,XB,FA,FB,SVUNIT)
      IMPLICIT NONE

      DOUBLE PRECISION, INTENT(IN) :: XA,XB,FA,FB
      CHARACTER(*),     INTENT(IN) :: SVUNIT

      WRITE(6,100) XA,XB,SVUNIT,FA,FB
  100 FORMAT(/2X,'VWDB BEGINS WITH RANGE: ',1PG17.10,' TO ',G17.10,1X,A,
     2       /2X,'   SMALLEST EIGENVALUE: ',G17.10,4X,G17.10)

      RETURN
      END
C================================================================ PRVWDB
      SUBROUTINE PRVWDB(XA,XB,XC,SVNAME,SVUNIT,METHOD,SVVAL)
C  PRINT METHOD USED FOR CURRENT STEP IN VWDB ALGORITHM, AND NEW POINT
      IMPLICIT NONE

      INTEGER,          PARAMETER  :: LTEXT=70
      DOUBLE PRECISION, INTENT(IN) :: SVVAL,XA,XB,XC
      CHARACTER(*),     INTENT(IN) :: SVNAME,SVUNIT,METHOD
      CHARACTER(LTEXT) SVTEMP
      INTEGER L1,L2

      SVTEMP=' '
      SVTEMP(1:7)='APPLIES'
      L1=LEN(TRIM(METHOD))+9
      SVTEMP(9:L1)=TRIM(METHOD)
      IF (METHOD.NE.'INV Q INTERP') THEN
        SVTEMP(L1+1:L1+24)='TO THE FIRST TWO POINTS'
        L1=L1+24
      ENDIF
      SVTEMP(L1+1:L1+12)='WHICH GIVES'
      L2=LTEXT-LEN(TRIM(SVNAME))+1
      SVTEMP(L2:LTEXT)=TRIM(SVNAME)

      WRITE(6,100) XA,XB,XC,TRIM(SVUNIT),SVTEMP,SVVAL,SVUNIT
  100 FORMAT(/2X,'VWDB WITH POINTS AT: ',1PG17.10,', ',G17.10,' AND ',
     1       G17.10,1X,A/2X,A,' = ',G17.10,1X,A)

      RETURN
      END
C================================================================ PRINCR
      SUBROUTINE PRINCR(FLDNEW,SVUNIT,METHOD,EIGMIN,NODE,
     1                  FSMALL,XSMALL,CONVGE)
      IMPLICIT NONE

      INTEGER,          INTENT(IN) :: NODE
      DOUBLE PRECISION, INTENT(IN) :: FLDNEW,EIGMIN,FSMALL,XSMALL
      CHARACTER(*),     INTENT(IN) :: SVUNIT,METHOD
      LOGICAL,          INTENT(IN) :: CONVGE

      WRITE(6,100) FLDNEW,TRIM(SVUNIT),TRIM(METHOD),EIGMIN
  100 FORMAT(/2X,'POINT AT ',1PG17.10,1X,A,' PREDICTED BY ',A,' GAVE ',
     1           'INCREASED MAGNITUDE OF SMALLEST EIGENVALUE OF '/
     2        2X,'MATCHING MATRIX  (VALUE WAS ',G12.5,')')

      IF (.NOT.CONVGE)
     1  WRITE(6,*)' CONVERGENCE CRITERION DTOL MAY BE TOO SMALL'

      WRITE(6,200) NODE,XSMALL,TRIM(SVUNIT),FSMALL
  200 FORMAT(/2X,'CONVERGENCE ON NODE ',I5,' TERMINATED AT ',1PG17.10,
     1        1X,A,' WHERE SMALLEST EIGENVALUE IS ',G12.5)

      RETURN
      END
C================================================================ PRCONV
      SUBROUTINE PRCONV(NSEEK,SVNAME,SVVAL,SVUNIT,NODE,ZCNTN,CONVGE,
     1                  METHOD)
C  PRINT LOCATION OF BOUND STATE, TOGETHER WITH CAVEATS IF ANY
      USE efvs, ONLY: EFV, ISVEFV
      IMPLICIT NONE

      INTEGER,          INTENT(IN) :: NODE,NSEEK
      DOUBLE PRECISION, INTENT(IN) :: SVVAL
      CHARACTER(*),     INTENT(IN) :: SVNAME,SVUNIT
      LOGICAL,          INTENT(IN) :: ZCNTN,CONVGE
      CHARACTER(*),     INTENT(IN) :: METHOD
      CHARACTER(36) SVTEMP

      SVTEMP=TRIM(SVNAME)
      SVTEMP=ADJUSTR(SVTEMP)

      IF (CONVGE) THEN
        IF (ZCNTN .AND. (NODE.EQ.NSEEK .OR. NODE.EQ.NSEEK-1)) THEN
          WRITE(6,100) NSEEK,SVTEMP,SVVAL,SVUNIT
        ELSEIF (NODE.NE.NSEEK .AND. NODE.NE.NSEEK-1) THEN
          WRITE(6,200) NSEEK,SVTEMP,SVVAL,TRIM(SVUNIT),NODE
        ELSE
          WRITE(6,300) NSEEK,SVTEMP,SVVAL,TRIM(SVUNIT)
        ENDIF
      ELSE
        WRITE(6,400) NSEEK,SVTEMP(2:36),SVVAL,TRIM(SVUNIT)
      ENDIF
  100 FORMAT(/2X,'CONVERGED ON NODE NUMBER ',I5,' AT ',A,' = ',
     1       1PG17.10,1X,A) !34+A1
  200 FORMAT(/2X,'CONVERGED ON NODE NUMBER ',I5,' AT ',A,' = ',
     1       1PG17.10,1X,A/'  BUT LAST NODE COUNT = ',0P,3X,I5,
     2       ' NOT AS DESIRED')
  300 FORMAT(/2X,'CONVERGED ON NODE NUMBER ',I5,' AT ',A,' = ',
     1       1PG17.10,1X,A,' BUT VARIABLE IS OUTSIDE RANGE')
  400 FORMAT(/2X,'NOT CONVERGED ON NODE NUMBER ',I5,'.',A,' = ',
     1       1PG17.10,1X,A,' IS CURRENT BEST ESTIMATE')

      IF (EFV(0).NE.1.D0 .AND. ISVEFV.NE.0)
     1  WRITE(6,*) ' WITH SCALING FACTOR',EFV(0)
      IF (TRIM(METHOD).EQ.'BISECTION')
     1  WRITE(6,*) ' NODE LOCATED BY BISECTION SO PROBABLE ERROR '//
     2             'IS DTOL/2'

      RETURN
      END
C================================================================ PRLAST
      SUBROUTINE PRLAST(SVSTEP,SVUNIT)
C  PRINT SIZE OF LAST STEP
      IMPLICIT NONE

      DOUBLE PRECISION, INTENT(IN) :: SVSTEP
      CHARACTER(*),     INTENT(IN) :: SVUNIT

      WRITE(6,100) SVSTEP,SVUNIT
  100 FORMAT(/2X,'LAST STEP WAS',60X,1PG17.10,1X,A) !13+20

      RETURN
      END
C================================================================ PRLOC
      SUBROUTINE PRLOC(TIME)
C  PRINT TIME TAKEN TO LOCATE BOUND STATE
      IMPLICIT NONE

      DOUBLE PRECISION, INTENT(IN) :: TIME

      WRITE(6,100) TIME
  100 FORMAT(/2X,'TIME TO LOCATE THIS NODE WAS ',F8.2,' CPU SECONDS')

      RETURN
      END
