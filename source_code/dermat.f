      SUBROUTINE DERMAT(IDER,W,N,R,P,VL,IV,CENT,
     1                  RMLMDA,MXLAM,NPOTL,IPRINT)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE potential
C
C  EVALUATES THE IDER'TH DERIVATIVE OF THE POTENTIAL AT RADIUS R
C  W = VCOUPL + VCENT
C  ORDER OF THE REAL SYMMETRIC MATRIX W IS N
C  THE FULL MATRIX IS COMPUTED
C  VL IS THE PREVIOUSLY COMPUTED MATRIX OF THE COUPLING POTENTIAL
C  IV IS AN INDEX ARRAY MAPPING P ONTO VL, SUCH THAT VL(I) IS
C        A COEFFICIENT TO MULTIPLY P(IV(I))
C  CENT(I) IS L*(L+1) FOR THE I-TH CHANNEL
C    (BUT VL ARRAY IS USED INSTEAD OF CENT IF NRSQ = 1)
C  RMLMDA IS THE CONVERSION FACTOR FROM EPSIL * CM-1 TO REDUCED ENERGY
C  UNITS, RMLMDA = MUNIT*URED*RUNIT**2*EPSIL/BFCT,
C  CONCEPTUALLY 2*MU/HBAR^2 IN APPROPRIATE UNITS
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      SAVE RSAVE
C
      DIMENSION W(N,N),VL(1),IV(1),CENT(N),P(MXLAM),IDUM1(1)
C
C  COMMON BLOCK FOR DERIVATIVES
      LOGICAL NUMDER
      COMMON /DERIVS/ NUMDER
C
C  STEP SIZE FOR NUMERICAL DERIVATIVES: SHOULD REALLY BE ADJUSTABLE
      DATA DEL / 1.D-3 /
C
      IF (IDER.EQ.1) RSQ=-2.D0/R**3
      IF (IDER.EQ.2) RSQ=+6.D0/R**4

      RSQ=MIN(RSQ,1.D16)
C
C  COMPUTE THE RADIAL PARTS OF THE POTENTIAL
C  IDUM1 AND IDUM2 ARE DUMMY ARGUMENTS HERE.
C
      IF (NUMDER) THEN
C
C  NUMERICAL DERIVATIVE OPTION.
C  NOTE THAT IF IDER = 2 THIS ASSUMES THAT
C  THE POTENTIAL ITSELF IS ALREADY IS ALREADY IN THE FIRST
C  MXLAM ELEMENTS OF P. THIS IS NOT TRUE IF DERMAT HAS BEEN
C  CALLED MORE RECENTLY THAN WAVMAT, SO THE IDER = 2 CALL
C  MUST PRECEDE THE IDER = 1 CALL.
C
C  FIRST SEE WHETHER DERMAT HAS BEEN CALLED BEFORE FOR THIS
C  VALUE OF R, AND IF SO SKIP POTENTIAL EVALUATIONS
C
        IPDIM=MXLAM+NCONST+NRSQ
C
        IF (R.NE.RSAVE) THEN
          RSAVE=R
          RR=R-DEL
          CALL POTENL(0,MXLAM,IDUM1,RR*RSCALE,P(IPDIM+1),IDUM2,IPRINT)
          RR=R+DEL
          CALL POTENL(0,MXLAM,IDUM1,RR*RSCALE,P(2*IPDIM+1),IDUM2,IPRINT)
        ENDIF
C
        DO 10 I=1,MXLAM
          P1=P(IPDIM+I)
          P2=P(2*IPDIM+I)
          IF (IDER.EQ.1) P(I) = (P2-P1)/(2.D0*DEL)
   10     IF (IDER.EQ.2) P(I) = (P2+P1-2.D0*P(I))/(DEL*DEL)
C
      ELSE
        CALL POTENL(IDER,MXLAM,IDUM1,R*RSCALE,P,IDUM2,IPRINT)
C  NEXT LINE NEEDED ONLY FOR BOUND, WHICH DOES NOT AT PRESENT (9/06)
C  USE PROPAGATORS THAT CALL DERMAT
C       CALL PERTRB(R*RSCALE,P,MXLAM,0)
      ENDIF
C
C  CONVERT POTENTIAL P FROM EPSIL * CM-1 TO REDUCED UNITS AND
C  ALSO SCALE IT BY ANY SCALING FACTOR IN SCALAM
C
      CALL SCAPOT(P,MXLAM,RMLMDA)
C
C  SPECIAL CODE INTRODUCED SEP 06 TO HANDLE OFF-DIAGONAL
C  TERMS IN THE THRESHOLD AND CENTRIFUGAL HAMILTONIANS.
C  THIS REPLACES IVLFL<0 IN MOLSCAT 14 AND BOUND 6
C  AND IS INTENDED TO BE COMPATIBLE WITH NON-TRIVIAL IV
C
      DO 18 I=1,NCONST
        P(MXLAM+I)=0.D0
   18 CONTINUE
      IF (NRSQ.GT.0) THEN
        DO I=1,NRSQ
          P(MXLAM+NCONST+I)=RSQ
        ENDDO
      ENDIF
C
      CALL WAVVEC(VL,P,IV,W,N,NPOTL)
C
C  NOW COMPUTE THE DIAGONAL CONTRIBUTIONS W(I,I).
C
      IF (NRSQ.EQ.0) THEN
        DO 20 I=1,N
          W(I,I) = W(I,I) + RSQ*CENT(I)
   20   CONTINUE
      ENDIF

      RETURN
      END
