      SUBROUTINE PSIASY(N,NOPEN,NB,SR,SI,REND,WVEC,L,LAMBDA,
     1                  PSIRE,PSIIM)
C  This subroutine is part of the MOLSCAT, BOUND and FIELD suite of programs
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  18 APRIL 2013 G. C. MCBANE.
C  MODIFIED 2 MAY TO REMOVE ASSUMPTION THAT OPEN CHANNELS ARE THE
C  LOW-NUMBERED ONES.
C  MODIFIED 15 MAY TO USE NB ARRAY CORRECTLY FOR INDEXING S.
C
C  CONSTRUCT ONE COLUMN OF ASYMPTOTIC WAVEFUNCTION X(R)
C  FOR A SCATTERING PROBLEM FROM SPECIFIED
C  INITIAL CHANNEL AND S-MATRIX
C  THIS VERSION USES SIMPLE EXPONENTIALS, NOT RICATTI-HANKEL FUNCTIONS, SO IT
C  CAN ONLY BE USED FOR L=0 OR FOR MATCHING AT VERY LONG RANGE WHERE THE
C  CENTRIFUGAL POTENTIAL HAS DIMINISHED SUFFICIENTLY.
C
C  IMPLEMENTS EQUATION (13) OF L. THOMAS, JCP 76, 4925 (1982)
C  RESULT IS RETURNED AS REAL AND IMAGINARY PARTS IN TWO SEPARATE ARRAYS

C  I THINK THIS SHOULD BE COMPATIBLE WITH NONZERO NCONST/NRSQ; IT RETURNS
C  THE ASYMPTOTIC PSI IN THE ASYMPTOTIC BASIS REPRESENTATION.  IT MAY NEED
C  TO BE TRANSFORMED TO THE PRIMITIVE BASIS FOR SOME USES.

      DIMENSION SR(NOPEN,NOPEN), SI(NOPEN,NOPEN), WVEC(N),L(N)
      DIMENSION PSIRE(N), PSIIM(N), NB(N)

      DOUBLE COMPLEX IU  ! IMAGINARY UNIT
      PARAMETER (IU = (0.0D0,1.0D0))
      DOUBLE COMPLEX TERM

      PIO2 = 0.5D0*ACOS(-1.0D0)

C  FIND INCOMING CHANNEL IN "S-MATRIX INDEX SCHEME"
      NL0 = 0
      DO I = 1,NOPEN
         IF (NB(I).EQ.LAMBDA) THEN
            NL0 = I
            EXIT
         ENDIF
      ENDDO
      IF (NL0.EQ.0) THEN
         WRITE(6,*) ' PSIASY: COULD NOT FIND ENTRANCE CHANNEL'
         STOP
      ENDIF

C  COMPUTE OUTGOING WAVE COMPONENT IN OPEN CHANNELS
      DO I = 1, NOPEN
         NX = NB(I)
         WK = WVEC(NX)
         WL = L(NX)
         WRITE(6,*) ' PSIASY: I, L(I) = ', I, WL
         TERM = (-1.0D0/SQRT(WK))*EXP(IU*(WK*REND-PIO2*WL))*
     1          (SR(I,NL0)+IU*SI(I,NL0))
         PSIRE(NX) = DREAL(TERM)
         PSIIM(NX) = DIMAG(TERM)
      ENDDO

C  ADD ZEROS IN CLOSED CHANNELS
      IF (N>NOPEN) THEN
         DO I = NOPEN+1,N
            NX = NB(I)
            PSIRE(NX) = 0.0D0
            PSIIM(NX) = 0.0D0
         ENDDO
      ENDIF

C  ADD INCOMING WAVE COMPONENT IN INITIAL CHANNEL.  WE DON'T NEED S MATRIX
C  HERE SO WE CAN USE ORDINARY INDEXING INTO WVEC AND L.
      WK = WVEC(LAMBDA)
      WL = L(LAMBDA)
      IF (WK.GE.0.0D0) THEN !INCOMING CHANNEL OPEN; OK
         TERM = (1.0D0/SQRT(WK))*EXP(-IU*(WK*REND-PIO2*WL))
         PSIRE(LAMBDA) = PSIRE(LAMBDA) + DREAL(TERM)
         PSIIM(LAMBDA) = PSIIM(LAMBDA) + DIMAG(TERM)
      ELSE !PROBLEM
         WRITE(6,*) ' PSIASY.F: INITIAL CHANNEL LAMBDA= ', LAMBDA
         WRITE(6,*) ' IS CLOSED.  STOPPING.'
         STOP
      ENDIF

      RETURN
      END
C ========================================================= END OF PSIASY
      SUBROUTINE PSIRH(N,NOPEN,NB,SR,SI,REND,WVEC,L,LAMBDA,
     1                 PSIRE,PSIIM)
C  This subroutine is part of the MOLSCAT, BOUND and FIELD suite of programs
C
C  18 APRIL 2013 G. C. MCBANE.
C
C  ALTERNATE VERSION THAT USES RICATTI-HANKEL FUNCTIONS
C  FOR BETTER ACCURACY WITH NONZERO L.

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)

      DIMENSION SR(NOPEN,NOPEN), SI(NOPEN,NOPEN), WVEC(N),L(N)
      DIMENSION PSIRE(N), PSIIM(N), NB(N)

      DOUBLE COMPLEX IU  ! IMAGINARY UNIT
      PARAMETER (IU = (0.0D0,1.0D0))
      DOUBLE COMPLEX TERM, HLP, HLM

      PIO2 = 0.5D0*ACOS(-1.0D0)

C  FIND INCOMING CHANNEL IN "S-MATRIX INDEX SCHEME"
      NL0 = 0
      DO I = 1,NOPEN
         IF (NB(I).EQ.LAMBDA) THEN
            NL0 = I
            EXIT
         ENDIF
      ENDDO
      IF (NL0.EQ.0) THEN
         WRITE(6,*) ' PSIRH: COULD NOT FIND ENTRANCE CHANNEL'
         STOP
      ENDIF

      WRITE(6,*) ' PSIRH: ENTRANCE CHANNEL IS NUMBER ', NL0
      WRITE(6,*) ' IN ENERGY '
      WRITE(6,*) ' WITH ORBITAL L =  ', L(LAMBDA)

      WRITE(6,*)
      WRITE(6,*) ' I, NX, PSIRE(NX), PSIIM(NX)  SR   SI'


C  COMPUTE OUTGOING WAVE COMPONENT IN OPEN CHANNELS
      DO I = 1, NOPEN
         NX = NB(I)  ! NX IS THE CHANNEL NUMBER IN ASYMPTOTIC INDEX SCHEME
                     ! FOR THE I'TH LOWEST ENERGY CHANNEL
         WK = WVEC(NX)
         WKR = WK*REND
         CALL RBESJY(DBLE(L(NX)),WKR,ZJN,ZJNP,ZYN,ZYNP)
         HLP = IU*ZJN-ZYN  ! RICATTI-HANKEL H_L^+ =  I*JHAT - YHAT
C  S-MATRIX IS ONLY DEFINED BETWEEN OPEN CHANNELS
         TERM = (-1.0D0/SQRT(WK))*HLP*(SR(I,NL0)+IU*SI(I,NL0))
         PSIRE(NX) = DREAL(TERM)
         PSIIM(NX) = DIMAG(TERM)
         WRITE(6,'(2X,I4,1X,I4,4(E18.8,1X))')
     1       I, NX, PSIRE(NX), PSIIM(NX), SR(I,NL0), SI(I,NL0)

         IF (NX.EQ.LAMBDA) THEN  ! THIS IS INITIAL CHANNEL;
                                 ! ADD INCOMING WAVE PART
            HLM = -IU*ZJN-ZYN ! RICATTI-HANKEL H_L^- =  -I*JHAT - YHAT
            TERM = (1.0D0/SQRT(WK))*HLM
            PSIRE(NX) = PSIRE(NX) + DREAL(TERM)
            PSIIM(NX) = PSIIM(NX) + DIMAG(TERM)
            WRITE(6,*) ' ADDING INCOMING PART:'
            WRITE(6,*) I, NX, DREAL(TERM), DIMAG(TERM)
            WRITE(6,*) ' TO GIVE FOR THIS CHANNEL'
            WRITE(6,*) I, NX, PSIRE(NX), PSIIM(NX)
         ENDIF

      ENDDO

C  ADD ZEROS IN CLOSED CHANNELS
C  COULD BE IMPROVED TO USE MODIFIED SPHERICAL BESSEL FUNCTIONS
      IF (N.GT.NOPEN) THEN
         DO I = NOPEN+1,N
            NX = NB(I)
            PSIRE(NX) = 0.0D0
            PSIIM(NX) = 0.0D0
         ENDDO
      ENDIF

      RETURN
      END
C ========================================================= END OF PSIRH
      SUBROUTINE PSIK(N,NOPEN,NB,K,REND,WVEC,L,LAMBDA,PSIRE)
C  This subroutine is part of the MOLSCAT, BOUND and FIELD suite of programs
C
C  18 APRIL 2013 G. C. MCBANE.
C
C  YET ANOTHER VERSION THAT USES "K-MATRIX BOUNDARY CONDITIONS"
C  PSI(R) = J(R) + N(R) K, AND RETURNS A REAL PSI.  NO NEED FOR
C  INCOMING CHANNEL HERE, BUT LAMBDA IS STILL NEEDED TO CHOOSE WHICH
C  COLUMN OF THE WAVEFUNCTION MATRIX WE RETURN.
C  BECAUSE I ONLY HAVE THE OPEN-OPEN BLOCK OF THE K MATRIX, I DO NOT
C  ASSIGN ACCURATE VALUES TO PSI IN CLOSED CHANNELS, BUT SET THEM TO ZERO.

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)

      DIMENSION  WVEC(N),L(N)
      DIMENSION PSIRE(N),  NB(N)
      DOUBLE PRECISION K(NOPEN,NOPEN)

C  FIND INCOMING CHANNEL IN "S-MATRIX INDEX SCHEME" (SAME AS K-MATRIX SCHEME)
      NL0 = 0
      DO I = 1,NOPEN
         IF (NB(I).EQ.LAMBDA) THEN
            NL0 = I
            EXIT
         ENDIF
      ENDDO
      IF (NL0.EQ.0) THEN
         WRITE(6,*) ' PSIK: COULD NOT FIND ENTRANCE CHANNEL'
         STOP
      ENDIF

C  COMPUTE NK COMPONENT IN OPEN CHANNELS.  NOTE THAT WHAT JOHNSON CALLS N,
C  ABRAMOWITZ AND STEGUN CALLS Y.
      WRITE(6,*) ' PSIK: I, K^{-1/2}, J, N, K(I,NL0), PSI:'
      DO I = 1, NOPEN
         NX = NB(I)
         WK = WVEC(NX)
         WKR = WK*REND
         CALL RBESJY(DBLE(L(NX)),WKR,ZJN,ZJNP,ZYN,ZYNP)
         TERM = (1.0D0/SQRT(WK)) * ZYN * K(I,NL0)
         WRITE(6,*) ' TERM1 = ', TERM
         PSIRE(NX) = TERM

         IF (NX.EQ.LAMBDA) THEN  ! THIS IS SELECTED CHANNEL; ADD J PART
            TERM = (1.0D0/SQRT(WK))*ZJN
            WRITE(6,*) ' TERM2 = ', TERM
            PSIRE(NX) = PSIRE(NX) + TERM
         ENDIF
         WRITE(6,'(2X,I1,1X,5(E12.4,1X))') I, 1.0D0/SQRT(WK),
     1                                     ZJN, ZYN, K(I,NL0), PSIRE(NX)

      ENDDO

C  ADD ZEROS IN CLOSED CHANNELS
      IF (N.GT.NOPEN) THEN
         DO I = NOPEN+1,N
            NX = NB(I)
            PSIRE(NX) = 0.0D0
         ENDDO
      ENDIF

      RETURN
      END
